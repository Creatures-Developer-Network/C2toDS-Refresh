*Create Signpost*

new: simp 1 3 50200 "moe_C2toDS_Fav" 1 0 1

attr 272

mvto 6753 48383

tick 10






*NeoDement: added an override for the default DS fav places script to move camera twice to account for bug when switching metaroom

**********************ICON EVENTS
******Icon clicked on
scrp 1 4 50200 1

*	dbg: asrt "signpost" = "fixed"

	inst
*	get your species number
	setv va00 spcs
*	find your counterpart signpost
	rtar 1 3 va00
	doif targ eq null
*	no targ, stop
		stop
	endi
*	move camera to them
	cmrt 0
	cmrt 0
*	give all agents a 'metaroom has changed' message
	enum 0 0 0
		mesg writ targ 900
	next

endm


*NeoDement: delete our override script if the global signpost Activate behaviour has been modified in any way

scrp 1 4 50200 10
	inst

*ONLY RUN OUR MODIFIED SCRIPT IF THE DEFAULT SIGNPOST BEHAVIOUR IS PRESENT!
*this should hopefully allow custom signpost systems to not break.
	sets va99 sorc 1 4 0 1

*erase whitespace from end of string
	doif subs va99 strl va99 1 = " "
		loop
			setv va80 strl va99
			subv va80 1
			sets va99 subs va99 1 va80
		untl subs va99 strl va99 1 <> " "
	endi

*look for an exact match of the default script
	doif va99 <> "inst setv va00 spcs rtar 1 3 va00 doif targ eq null stop endi cmrt 0 enum 0 0 0 mesg writ targ 900 next"
		scrx 1 4 50200 1
*		dbg: asrt "script" = "wrong"
	endi

endm





rscr

enum 1 3 50200

	kill targ

next

*Delete Signpost*

doif totl 1 4 50200 gt 0

	subv game "ds_favourites" 1

	enum 1 4 50200

		setv va50 ov50

		kill targ

	next

	enum 1 4 0

		doif ov50 gt va50

			subv ov50 1

			setv va02 ov50

			mulv va02 50

			addv va02 145

			flto va02 0

		endi

	next

endi