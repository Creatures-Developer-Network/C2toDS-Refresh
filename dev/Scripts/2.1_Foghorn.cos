* 2 21 50212 - Foghorn
* 1 23 50212 - Musical Notes
* 1 23 50213 - Foghorn Fog

* Yay, horn!

* Currently clicking anywhere on the horn causes it to activate. Not sure how to change this,

* or even if we want to. I think it's more usable if you can click anywhere, but that's just

* my opinion :)



* Pretty much straight-converted from c2. 


*NeoDement made this pickupable, added a hit script and fixed some potential FRAT issues.
*Foghorn Fog sprites are now fully opaque instead of using dithered transparency. ALPH transparency is applied to them randomly when created, to get a similar effect.
*Tweaked timing and angles of notes and fog and stuff



new: comp 2 21 50212 "moe_C2toDS_foghorn" 34 0 50

*on the dock to the left of the volcano
mvto 9090 48145



*Foghorn Constructor Script

scrp 2 21 50212 10

*attr 4

*Mouseable + Activateable + Suffer Collisions + Suffer Physics
	attr 198

*Activate 1 + Activate 2 + Hit
	bhvr 11


*physics!
	accg 2

	fric 100

	perm 51

	elas 0


	puhl -1 155 55

	rnge 500


	pat: dull 1 "moe_C2toDS_foghorn" 1 48 0 50

	pat: dull 2 "moe_C2toDS_foghorn" 4 204 0 50

	pat: dull 3 "moe_C2toDS_foghorn" 7 176 0 50

	pat: dull 4 "moe_C2toDS_foghorn" 10 152 0 50


*C2 Framerate
	frat 2
	part 1
	frat 2
	part 2
	frat 2
	part 3
	frat 2
	part 4
	frat 2

endm




scrp 2 21 50212 1

	inst

	doif crea from eq 1
		stim writ from 97 1
	endi

	setv va00 posl

	setv va01 post

	lock
	slow

	part 1

	anim [ 0 1 2 1 255]

	setv va02 rand 0 1

	doif va02 eq 0

		snde "hrn1"
		setv va08 11

	else

		snde "hrn2"
		setv va08 8

	endi

	reps va08

		setv va03 rand 2 4

		part va03

		anim [ 0 1 2 1 0 ]

		wait 1

		inst


		doif va09 = 0

*Musical Notes
			new: simp 1 23 50212 "moe_C2toDS_foghorn" 12 13 7000

			attr 16

			setv va02 rand 0 2

			mulv va02 4

			base va02

			pose 0

			mvto va00 va01

			slow

			setv vely -1

*better angle randomization for the notes
			setv va07 rand -200 200

			divv va07 100.0

			setv velx va07

			mesg writ targ 8


			setv va09 1

		else

			setv va04 va00

			setv va06 rand 30 80

			addv va04 va06

			setv va05 va01

			addv va05 rand -4 4




*Fog
			new: simp 1 23 50213 "moe_C2toDS_foghorn" 9 25 9000

			attr 64

			mvto va04 va05

*		setv va02 rand 0 -1

*better angle randomization for the notes
			setv va02 rand -20 5

			divv va02 100.0

			velo 1 va02

			slow

			setv va09 0

		endi

		targ ownr

		wait 10

	repe

	part 1

	pose 0

	unlk

endm



scrp 1 23 50212 10

	frat rand 2 4

	reps rand 10 25

		anim [ 0 1 2 1 ]

		over

	repe

	setv va00 0

*notes alpha fade out (plays anim one more time)
	alph 128 1

	anim [ 0 1 2 1 ]

	over

	kill ownr

endm



scrp 1 23 50213 10

*random transparency
	setv va00 rand 50 150

	alph va00 1

	frat rand 2 6

	anim [ 0 1 2 ]

	reps rand 12 25

		anim [ 3 4 5 4 ]

		over

	repe

*make it a bit more transparent before it disappears
	mulv va00 1.25

	doif va00 >= 255
		setv va00 255
	endi

	alph va00 1

	anim [ 3 4 5 6 7 8 ]

	over

	kill ownr

endm



scrp 2 21 50212 2

	inst

	doif crea from eq 1
		stim writ from 97 1
	endi

	mesg writ ownr 0

endm


scrp 2 21 50212 3

	inst

*Stimulate the creature with "Played with toy"
	doif crea from eq 1
		stim writ from 97 1
	endi

	sndc "hit_"
	velo rand -2 2 rand -4 -7

endm






rscr


enum 2 21 50212

	kill targ

next

enum 1 23 50212

	kill targ

next

enum 1 23 50213

	kill targ

next

scrx 2 21 50212 1

scrx 2 21 50212 2

scrx 1 23 50212 10

scrx 1 23 50213 10