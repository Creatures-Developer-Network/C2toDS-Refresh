*//*Anica Berry*//*

* 2 6 50208 - Arnica Berry Leaf
* 2 8 50208 - Arnica Berry

*Matthew, "Moe", '10*
*The one who screams removed empty hunger decreases, made berries invisible until picked,
*and stopped berries from dying on the vine.
*NeoDement fixed plane issues
* (if you worked on this file, feel free to add your name!)

new: simp 2 6 50208 "moe_C2toDS_berries" 0 0 5000

attr 16

mvto 3782 47659

tick 1

scrp 2 6 50208 9

	tick rand 100 200

	doif totl 2 8 50208 lt 15

*	setv va00 2

		setv va00 rand 0 1

		doif va00 eq 0

			setv va01 rand 3985 4021

			setv va02 rand 47837 48051

		elif va00 eq 1

			setv va01 rand 3730 3814

			setv va02 rand 47600 47835

		endi

		gsub makearnicaberries

	endi



	subr makearnicaberries

		new: simp 2 8 50208 "moe_C2toDS_berries" 0 6 rand 3500 5000

		mvsf va01 va02

		setv ov01 1

		accg 0

	retn

endm



scrp 2 8 50208 10

	perm rand 45 55

	bhvr 48

	attr 211

	elas 10

	fric 50

	tick rand 500 1000

*emit 6 .15

	wait 1

	doif ov01 eq 0

		accg 4

	else

		accg 0

	endi

endm



scrp 2 8 50208 9

	doif carr eq null

		addv ov00 1

		doif ov01 eq 1

			doif ov00 ge rand 30 50

				accg 4

				attr 195

				emit 6 .15

				velo rand -5 5 0

				setv ov01 0

				setv ov00 0
			endi

		elif ov00 ge rand 3 5

			kill targ

		endi

	endi

endm



*scrp 2 8 50208 1
*
*	mesg writ targ 12
*
*endm



*scrp 2 8 50208 4
*
** just gonna set the plane when we create it instead
**	plne rand 250 500
*
*endm



scrp 2 8 50208 5

	accg 4

	attr 195

	emit 6 .15

endm



scrp 2 8 50208 6

	sndc "dr12"

endm





scrp 2 8 50208 12

	sndc "chwp"

*Arnica*

	stim writ from 78 1

	targ from

	chem 94 .38

*Downatrophin*

	chem 17 .78

*Starch*

*	chem 5 .19

*Hunger for carbs Decrease*

*chem 150 -.19

	targ ownr

	wait 1

	kill targ

endm

rscr


enum 2 6 50208

	kill targ

next

enum 2 8 50208

	kill targ

next

scrx 2 6 50208 9

scrx 2 8 50208 9

scrx 2 8 50208 6

scrx 2 8 50208 5

scrx 2 8 50208 12