* 2 21 50203 - Punching bag


*NeoDement enabled picking up punching bags and tweaked the left-sided slap anim to match the right one more closely.


new: simp 2 21 50203 "moe_C2toDS_toys" 128 8 800

mvsf 6932 48690



* Volcano

new: simp 2 21 50203 "moe_C2toDS_toys" 128 8 800

mvsf 9329 48824





* Creation script

scrp 2 21 50203 10

*	attr 196
	attr 198

	core 0 88 35 93

	bhvr 11

	accg 2

	fric 100

	perm 51

	elas 0

	base 0

	frat 2

endm



* Activate 1 script

scrp 2 21 50203 1

	lock

	stim writ from 97 1

	targ from

	setv va00 posl
*slight bias toward right to left side slaps. nicer anim.
	addv va00 6

	targ ownr

	doif posx gt va00

		setv va00 1

	else

		setv va00 0

	endi

	doif va00 eq 0

*Added pointer smack animations to fit the direction hit. -- Moe 

		doif from eq pntr

			targ pntr

			anim [4 4 5 5 0]

			targ ownr

		endi

		anim [10 11 12 13 14 15 16 17 0]

		snde "pb1_"

		wait 20

	else

		doif from eq pntr

			targ pntr

			anim [11 11 12 12 0]
			targ ownr

		endi

		anim [1 2 3 4 5 6 7 8 9 0]

		snde "pb2_"

		wait 20

	endi

	unlk

endm



* Activate 2 script

scrp 2 21 50203 2

	lock

	stim writ from 97 1

	targ from

	setv va00 posl

	targ ownr

	doif posx gt va00

		setv va00 1

	else

		setv va00 0

	endi

	doif va00 eq 0

		anim [10 11 12 13 14 15 16 17 0]

		snde "pb1_"

		wait 20

	else

		anim [1 2 3 4 5 6 7 8 9 0]

		snde "pb2_"

		wait 20

	endi

	unlk

endm



* Hit script

scrp 2 21 50203 3

	lock

	stim writ from 97 1

	targ from

	setv va00 posl

	targ ownr

	doif posx gt va00

		setv va00 1

	else

		setv va00 0

	endi

	doif va00 eq 0

		anim [10 11 12 13 14 15 16 17 0]

		wait 20

	else

		anim [1 2 3 4 5 6 7 8 9 0]

		wait 20

	endi

	unlk

endm





**##Remover Scripts##**

rscr



enum 2 21 50203

	kill targ

next