* Some fixes by NeoDement - The Terrarium door now remembers the correct pose when changing to/from the night gallery, the trolly cable overlays now have previously missing night galleries. Fixed some overlays disappearing at night. Fixed main bg changing before overlays and world wrap camera bgs can load.

******************
* Night switcher *
******************

new: simp 1 1 50200 "blnk" 0 0 0
mvto 5900 47630
setv ov01 gmap posx posy
addb ov01 "c2tods"
addb ov01 "c2tods_night"
tick 200

*debug keys b/n to toggle night/day mode
imsk 1


* ov00 0 = day
* ov00 1 = night

scrp 1 1 50200 10

	doif time eq 3 or time eq 4
		setv ov00 0
	else
		setv ov00 1
	endi

endm


*debug keys b/n to toggle night/day mode
scrp 1 1 50200 73
	doif _p1_ = 66
		setv ov00 0
		setv ov99 1
		call 9 0 0
	elif _p1_ = 78
		setv ov00 1
		setv ov99 2
		call 9 0 0
	endi
endm



scrp 1 1 50200 9
	inst
	lock

	doif game "C2toDS_DisableNightVisuals" = 1
		gsub day
		tick 0
		stop
	endi

*debug keys b/n to toggle night/day mode
	doif ov99 eq 1
		gsub night
		stop
	elif ov99 eq 2
		gsub day
		stop
	endi
	setv ov99 0

	doif ov00 eq 0
		doif time eq 3 or time eq 4
			gsub night
		endi
	elif ov00 eq 1
		doif time eq 0 or time eq 1 or time eq 2
			gsub day
		endi
	endi


	subr night

		inst


************************** Terrarium door **************************
		enum 2 2 50201
*show nighttime version of door, hide daytime version
			doif gall = "moe_c2tods_doors_night"
				show 1
			else
				show 0
			endi
		next


************************** World tree leaves **************************
		enum 2 6 50200
			doif gall eq "moe_c2tods_tre2"
				setv va00 abba
				setv va01 pose
				gall "moe_c2tods_tre2_night" va00
				pose va01
			elif gall eq "moe_c2tods_tre3"
				setv va00 abba
				setv va01 pose
				gall "moe_c2tods_tre3_night" va00
				pose va01
			endi
		next


************************** Boat **************************
		enum 3 1 50201
			part 1
			gall "moe_c2tods_boat_night" 1
*hide daytime oar+button
			part 2
			alph 256 1
			part 4
			alph 256 1
*show nighttime oar+button
			part 3
			alph 0 0
			part 5
			alph 0 0
		next


************************** Dingy (aka yellow raft) **************************
		enum 3 1 50203
			part 1
			gall "moe_c2tods_dingy_night" 1
*hide daytime left+right rip
			part 2
			alph 256 1
			part 4
			alph 256 1
*show nighttime left+right rip
			part 3
			alph 0 0
			part 5
			alph 0 0
		next




*		enum 1 8 50200
		enum 1 0 50200

*slight adjustment to compensate for World Wrap Overlay Copies (see 1.2_World_Overlays)
			doif gnus = 8 or gnus = 30

*if it's a compound object, select the visual part
				doif type targ = 5
					doif part 1 = 1
						part 1
					endi
				endi

************************** Terrarium door screw **************************
				doif gall eq "moe_c2tods_doors" and abba eq 11
					setv va00 abba
					gall "moe_c2tods_doors_night" va00
				endi

************************** Waterfalls **************************
				doif gall eq "moe_c2tods_waterfalls"
					setv va00 abba
					gall "moe_c2tods_waterfalls_night" va00
				endi


************************** Waves **************************
				doif gall eq "moe_c2tods_waves"
					setv va00 abba
					gall "moe_c2tods_waves_night" va00
				endi

************************** Overlays **************************
				doif gall eq "moe_c2tods_overlays"
					doif abba lt 17 or abba gt 25 or abba eq 23
*fix for some overlays going invisible at night
						doif abba ne 35 and abba lt 38
							setv va00 abba
							gall "moe_c2tods_overlays_night" va00
						endi
					endi
				endi

************************** Overlays2 **************************
				doif gall eq "moe_c2tods_overlays2"
					doif abba lt 9 or abba eq 13 or abba eq 14 or abba eq 25 or abba eq 26 or abba eq 27 or abba eq 28 or abba eq 35 or abba eq 36
*(don't change the overlays specified above into night overlays)
					else
						setv va00 abba
						gall "moe_c2tods_overlays2_night" va00
					endi
				endi

************************** Jungle Overlays **************************
				doif gall eq "moe_c2tods_overlaysjungle"
					doif abba gt 2 and abba lt 17
						setv va00 abba
						gall "moe_c2tods_overlaysjungle_night" va00
					endi
				endi

			endi

		next

************************** Trolly Overlays **************************
		enum 1 8 50202
			doif abba = 5 or abba = 6
				setv va00 abba
				gall "moe_c2tods_trolly_night" va00
			endi
		next


*change world wrap camera background
		enum 1 5 50200
			scam targ 1
			bkgd mv01 "c2tods_night" 2
		next

		targ ownr
		setv ov00 1

*change main metaroom background (do this last because it forces the screen to update mid-inst otherwise)
		scam null -1
		bkgd ov01 "c2tods_night" 2

	retn


	subr day

		inst

************************** Terrarium door **************************
		enum 2 2 50201
*show daytime version of door, hide nighttime version
			doif gall = "moe_c2tods_doors"
				show 1
			else
				show 0
			endi
		next

************************** World tree leaves **************************
		enum 2 6 50200
			doif gall eq "moe_c2tods_tre2_night"
				setv va00 abba
				setv va01 pose
				gall "moe_c2tods_tre2" va00
				pose va01
			elif gall eq "moe_c2tods_tre3_night"
				setv va00 abba
				setv va01 pose
				gall "moe_c2tods_tre3" va00
				pose va01
			endi
		next


************************** Boat **************************
		enum 3 1 50201
			part 1
			gall "moe_c2tods_boat" 1
*show daytime oar+button
			part 2
			alph 0 0
			part 4
			alph 0 0
*hide nighttime oar+button
			part 3
			alph 256 1
			part 5
			alph 256 1
		next


************************** Dingy (aka yellow raft) **************************
		enum 3 1 50203
			part 1
			gall "moe_c2tods_dingy" 1
*show daytime left+right rip
			part 2
			alph 0 0
			part 4
			alph 0 0
*hide nighttime left+right rip
			part 3
			alph 256 1
			part 5
			alph 256 1
		next



*		enum 1 8 50200
		enum 1 0 50200

*slight adjustment to compensate for World Wrap Overlay Copies (see 1.2_World_Overlays)
			doif gnus = 8 or gnus = 30

*if it's a compound object, select the visual part
				doif type targ = 5
					doif part 1 = 1
						part 1
					endi
				endi

************************** Terrarium door screw **************************
				doif gall eq "moe_c2tods_doors_night" and abba eq 11
					setv va00 abba
					gall "moe_c2tods_doors" va00
				endi

************************** Waterfalls **************************
				doif gall eq "moe_c2tods_waterfalls_night"
					setv va00 abba
					gall "moe_c2tods_waterfalls" va00
				endi


************************** Waves **************************
				doif gall eq "moe_c2tods_waves_night"
					setv va00 abba
					gall "moe_c2tods_waves" va00
				endi

************************** Overlays **************************
				doif gall eq "moe_c2tods_overlays_night"
					doif abba lt 17 or abba gt 25 or abba eq 23
*fix for some overlays going invisible at night
						doif abba ne 35 and abba lt 38
							setv va00 abba
							gall "moe_c2tods_overlays" va00
						endi
					endi
				endi

************************** Overlays2 **************************
				doif gall eq "moe_c2tods_overlays2_night"
					doif abba lt 9 or abba eq 13 or abba eq 14 or abba eq 25 or abba eq 26 or abba eq 27 or abba eq 28 or abba eq 35 or abba eq 36
*(don't change the overlays specified above into night overlays)
					else
						setv va00 abba
						gall "moe_c2tods_overlays2" va00
					endi
				endi

************************** Jungle Overlays **************************
				doif gall eq "moe_c2tods_overlaysjungle_night"
					doif abba gt 2 and abba lt 17
						setv va00 abba
						gall "moe_c2tods_overlaysjungle" va00
					endi
				endi

			endi

		next

************************** Trolly Overlays **************************
		enum 1 8 50202
			doif abba = 5 or abba = 6
				setv va00 abba
				gall "moe_c2tods_trolly" va00
			endi
		next


*change world wrap camera background
		enum 1 5 50200
			scam targ 1
			bkgd mv01 "c2tods" 2
		next

		targ ownr
		setv ov00 0

*change main metaroom background (do this last because it forces the screen to update mid-inst otherwise)
		scam null -1
		bkgd ov01 "c2tods" 2

	retn

endm


rscr
enum 1 1 50200
	kill targ
next
scrx 1 1 50200 9