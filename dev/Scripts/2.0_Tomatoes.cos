* 2 4 50200 - Tomato Plant
* 2 8 50200 - Tomato

* Initial Conversion By Moe
* Instinct friendliness and survivability edits by the one who screams i guess
* (if you worked on this file, feel free to add your name!)

reps rand 5 7

	new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

	mvto rand 7159 7548 48380

	pose rand 0 6

repe



reps rand 5 7

	setv va00 rand 0 6

	new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

	mvto rand 3703 4000 48785

	pose rand 0 6

repe



reps rand 2 4

	setv va00 rand 0 6

	new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

	mvto rand 7785 7907 48712

	pose rand 0 6

repe

scrp 2 4 50200 10

	inst

	attr 208

	elas 0

	fric 60

	perm 60

	accg 4

	tick rand 150 250

	seta ov01 null

	seta ov02 null

	seta ov03 null

	mira rand 0 1

endm



scrp 2 4 50200 9

	doif pose lt 6

		tick rand 150 250

		setv va00 pose

		addv va00 1

		pose va00

		stop

	elif pose eq 6

		tick rand 350 450

		inst

		pose 7

		doif ov01 eq null

			gsub getposxposy

			gsub lefttom

			pose 3

			targ ownr

		endi

		doif ov02 eq null

			gsub getposxposy

			gsub middletom

			pose 3

			targ ownr

		endi



		doif ov03 eq null

			gsub getposxposy

			gsub righttom

			pose 3

			targ ownr

		endi

		slow

	endi





	doif ov01 eq null

		gsub getposxposy

		gsub lefttom

		targ ownr

	endi

	doif ov02 eq null

		gsub getposxposy

		gsub middletom

		targ ownr

	endi



	doif ov03 eq null

		gsub getposxposy

		gsub righttom

		targ ownr

	endi



	doif time eq 4

		stop

	endi



	addv ov10 1



	doif ov10 ge 40 and ov10 lt 45

		doif ov01 ne null

			targ ov01

			doif pose eq 3

				mesg writ targ 1001

				velo rand -5 2 -5

			endi

			targ ownr

			stop

		endi

		doif ov02 ne null

			targ ov02

			doif pose eq 3

				mesg writ targ 1001

				velo rand -5 2 -5

			endi

			targ ownr

			stop

		endi

		doif ov03 ne null

			targ ov03

			doif pose eq 3

				mesg writ targ 1001

				velo rand -5 2 -5

			endi

			targ ownr

			stop

		endi

	endi

	doif ov10 ge 50

		doif ov01 ne null and ov02 ne null and ov03 ne null

			inst

			lock

			targ ov01

			kill targ

			targ ownr

			targ ov02

			kill targ

			targ ownr

			targ ov03

			kill targ

			targ ownr

			frat 200

			pose 6

			anim [8 9 10 11 12 13 14]

			over

			kill ownr

			lock

		endi

	endi



	subr getposxposy

		setv va00 posl

		setv va01 post

		setv va02 plne

		setv va03 mira

	retn



	subr lefttom

		inst

		addv va01 8

		doif mira eq 0

			addv va00 1

		else

			addv va00 25

		endi

		new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 va02

		mira va03

		frel ownr

		mvto va00 va01

		setv ov00 1

		seta mv01 targ

		seta ov01 ownr

		setv ov02 1

	retn



	subr middletom

		inst

		addv va01 25

		doif mira eq 0

			addv va00 24

		else

			addv va00 2

		endi

		new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 va02

		mira va03

		base 4

		pose 0

		frel ownr

		mvto va00 va01

		setv ov00 1

		seta mv02 targ

		seta ov01 ownr

		setv ov02 2

	retn



	subr righttom

		inst

		addv va01 14

		subv va02 1

		doif mira eq 0

			addv va00 30

		else

			addv va00 5

		endi

		new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 va02

		mira va03

		base 8

		pose 0

		frel ownr

		mvto va00 va01

		setv ov00 1

		seta mv03 targ

		seta ov01 ownr

		setv ov02 3

	retn

endm



scrp 2 8 50200 10

	inst

	perm 51

	elas 25

	aero 3

	puhl -1 24 16

	wait 1

	doif ov00 eq 1

		tick rand 125 250

*attr 228

		attr 48

	else

		tick rand 1000 2000

*attr 231

		pose 4

		base 12

		pose 0

		attr 195

		accg 3

	endi

	emit 6 0.15

endm



scrp 2 8 50200 4

	inst

	call 1001 0 0

endm



scrp 2 8 50200 1001

	inst

	doif ov00 eq 1

		inst

		bhvr 48

		base 12

		pose 0

		attr 195

		accg 3

		tick rand 1000 2500

		setv ov00 0

		doif ov02 eq 1

			targ ov01

			seta ov01 null

			targ ownr

		elif ov02 eq 2

			targ ov01

			seta ov02 null

			targ ownr

		elif ov02 eq 3

			targ ov01

			seta ov03 null

			targ ownr

		endi

		doif carr eq null

			setv va00 posl

			subv va00 10

			mvsf va00 post

			perm 51

		endi

		slow

	endi

endm



scrp 2 8 50200 9



	doif time eq 4

		stop

	endi



	doif ov00 eq 1

		doif pose lt 3

			setv va00 pose

			addv va00 1

			pose va00

			stop

		else

			bhvr 48

			attr 227

		endi

	elif ov00 eq 0

		doif carr ne null or fall eq 1

			stop

		endi

		doif time ne 4 and time ne 0

			lock

			frat 75

			attr 211

			anim [0 1 2 3 4]

			over

			wait 75

			gsub check

			unlk

		endi

	endi



	subr check

		setv va20 grap posx posy



		setv va21 prop va20 1

		setv va22 prop va20 2

		setv va23 prop va20 3

		setv va24 prop va20 4

		setv va26 rtyp va20



		doif va26 ge 8 and va26 le 9

			kill targ

		endi


		inst

		esee 2 4 50200
*don't factor old dying plants in the population check pls -t1ws
			doif targ ne null and ov10 lt 40

				addv va25 1

			endi

		next

		slow

		targ ownr

		doif va25 lt 4

			doif va21 gt .302157 and va22 ge .254510 and va23 ge .254510 and va24 ge .254510

				gsub grow

			else

				kill targ

			endi

		else

			kill targ

		endi



	retn



	subr grow

		setv va00 posl

		setv va01 post

		new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

		mvsf va00 va01

		kill ownr

	retn



endm



scrp 2 8 50200 12

	inst

	sndc "chwp"


	stim writ from 78 1

	wait 1

	targ from

*chem 3 0.5

*chem 5 0.5

*chem 12 0.5

*	chem 149 -.25

	wait 1

	targ ownr

	doif base ne 17
		base 17
		pose 0
		attr 211
		emit 6 0
	else
		kill ownr
	endi

endm



scrp 2 8 50200 1

*mesg writ targ 12

endm



scrp 2 8 50200 6

	sndc "dr12"

endm

rscr


enum 2 4 50200

	kill targ

next

enum 2 8 50200

	kill targ

next


scrx 2 4 50200 10
scrx 2 4 50200 9

scrx 2 8 50200 10
scrx 2 8 50200 4
scrx 2 8 50200 1001
scrx 2 8 50200 9
scrx 2 8 50200 12
scrx 2 8 50200 1
scrx 2 8 50200 6