* 2 4 50200 - Tomato Plant
* 2 8 50200 - Tomato
* 2 23 50210 - Tomato Machine

* Initial Conversion By Moe
* Instinct friendliness and survivability edits by the one who screams i guess
* Some fixes by NeoDement + added Tomato Machine
* (if you worked on this file, feel free to add your name!)

* Some guesses about what these OVs are (on the plant):
* ov01 - Agent representing Left tomato
* ov02 - Agent representing Middle tomato
* ov03 - Agent representing Right tomato
* ov10 - Age of the plant (?) At 40-45, starts dropping its fruit, dies at age 50

*Tomato Fruit script 1001 seems to be the "I have been picked/plant has dropped me" script
* Tomato Fruit ov00 (int) is either state 1 (on the plant) or 0 (off the plant)
* Tomato Fruit ov01 (agent) is the plant that it came from.
* Tomato Fruit ov02 (int) is the number of tomato that they were (1 Left 2 Middle 3 Right)




*Install


*Tomato Plants

reps rand 5 7

	new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

	mvto rand 7159 7548 48380

	pose rand 0 6

repe



reps rand 5 7

	setv va00 rand 0 6

	new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

	mvto rand 3703 4000 48785

	pose rand 0 6

repe



reps rand 2 4

	setv va00 rand 0 6

	new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

	mvto rand 7785 7907 48712

	pose rand 0 6

repe


* Tomato machine

inst

* Genus Vendor, Plane 400
new: comp 2 23 50210 "moe_C2toDS_TomatoMachine" 0 0 400

*output (yellow)
prt: onew 0 "output" "Tomato Machine output" 17 84
*input (red)
prt: inew 0 "input" "Tomato Machine input" 27 84 1000

*Mouseable + Activateable + Suffer Collisions + Suffer Physics
attr 198

*Activate 1 + Activate 2 + Hit
bhvr 11

*physics
perm 51
elas 25
accg 5
aero 5
fric 100

*C2 framerate
frat 2

*it's in the terrarium beneath the dark blue ocean for now. feel free to move it elsewhere if it gets in the way.
mvto 2502 49025




*Tomato Plant Constructor Script

scrp 2 4 50200 10

	inst

	attr 208

	elas 0

	fric 60

	perm 60

	accg 4

*very low framerate for the grow anim
	frat 200

	tick rand 150 250

	seta ov01 null

	seta ov02 null

	seta ov03 null

	mira rand 0 1

endm


*Tomato Plant Timer Script

scrp 2 4 50200 9

	doif pose lt 6

		tick rand 150 250

		setv va00 pose

		addv va00 1

		pose va00

		stop

	elif pose eq 6

		tick rand 350 450

		inst

		pose 7

		doif ov01 eq null

			gsub getposxposy

			gsub lefttom

			pose 3

			targ ownr

		endi

		doif ov02 eq null

			gsub getposxposy

			gsub middletom

			pose 3

			targ ownr

		endi



		doif ov03 eq null

			gsub getposxposy

			gsub righttom

			pose 3

			targ ownr

		endi

		slow

	endi





	doif ov01 eq null

		gsub getposxposy

		gsub lefttom

		targ ownr

	endi

	doif ov02 eq null

		gsub getposxposy

		gsub middletom

		targ ownr

	endi



	doif ov03 eq null

		gsub getposxposy

		gsub righttom

		targ ownr

	endi



	doif time eq 4

		stop

	endi



	addv ov10 1



	doif ov10 ge 40 and ov10 lt 45

		doif ov01 ne null

			targ ov01

			doif pose eq 3

				mesg writ targ 1001

				velo rand -5 2 -5

			endi

			targ ownr

			stop

		endi

		doif ov02 ne null

			targ ov02

			doif pose eq 3

				mesg writ targ 1001

				velo rand -5 2 -5

			endi

			targ ownr

			stop

		endi

		doif ov03 ne null

			targ ov03

			doif pose eq 3

				mesg writ targ 1001

				velo rand -5 2 -5

			endi

			targ ownr

			stop

		endi

	endi

	doif ov10 ge 50

		inst

		doif ov01 ne null and ov02 ne null and ov03 ne null

			lock

			targ ov01

			kill targ

			targ ownr

			targ ov02

			kill targ

			targ ownr

			targ ov03

			kill targ

			targ ownr

			pose 6

*play (VERY slow) grow anim

			anim [8 9 10 11 12 13 14]

			over

			kill ownr

			lock

		endi

	endi



	subr getposxposy

		setv va00 posl

		setv va01 post

		setv va02 plne

		setv va03 mira

	retn



	subr lefttom

		inst

		addv va01 8

		doif mira eq 0

			addv va00 1

		else

			addv va00 25

		endi

		new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 va02

		mira va03

		frel ownr

		mvto va00 va01

		setv ov00 1

		seta mv01 targ

		seta ov01 ownr

		setv ov02 1

	retn



	subr middletom

		inst

		addv va01 25

		doif mira eq 0

			addv va00 24

		else

			addv va00 2

		endi

		new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 va02

		mira va03

		base 4

		pose 0

		frel ownr

		mvto va00 va01

		setv ov00 1

		seta mv02 targ

		seta ov01 ownr

		setv ov02 2

	retn



	subr righttom

		inst

		addv va01 14

		subv va02 1

		doif mira eq 0

			addv va00 30

		else

			addv va00 5

		endi

		new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 va02

		mira va03

		base 8

		pose 0

		frel ownr

		mvto va00 va01

		setv ov00 1

		seta mv03 targ

		seta ov01 ownr

		setv ov02 3

	retn

endm


*Tomato Constructor Script

scrp 2 8 50200 10

	inst

	core 4 24 11 35

	perm 51

	elas 25

	aero 3

	puhl -1 24 16

*need to wait a tick or ov00 is read before it's set
	wait 1
	inst

	doif ov00 eq 1

		tick rand 125 250

*attr 228

		attr 48

	else

		tick rand 1000 2000

*attr 231

		pose 4

		base 12

		pose 0

		attr 195

		accg 3

	endi

	emit 6 0.15

*low framerate for decay anim
	frat 75

endm


*Tomato Pickup Script

scrp 2 8 50200 4

	inst

	call 1001 0 0

endm


* Tomato being picked (or old plant dropping it)
scrp 2 8 50200 1001

	inst

*ov00 1 = still growing on the plant

	doif ov00 eq 1

		inst

		bhvr 48

		base 12

		pose 0

		attr 195

		accg 3

		tick rand 1000 2500

		setv ov00 0

* tell the mother plant you are being picked by blanking their OV for you

* the fact that I am having to write in targ eq null checks here makes me wonder 
* what situation this script is being triggered wherein a tomato is being 'picked' but there is no longer
* a mother plant... there might actually be a deeper issue here, or maybe not.
		targ ov01

		doif targ ne null

			doif mv02 eq 1

				seta ov01 null

			elif mv02 eq 2

				seta ov02 null

			elif mv02 eq 3

				seta ov03 null
			endi

		endi

		targ ownr

		doif carr eq null

			setv va00 posl

			subv va00 10

			mvsf va00 post

			perm 51

		endi

		slow

	endi

endm


* Tomato Timer Script

scrp 2 8 50200 9


*Don't do anything at night
	doif time eq 4

		stop

	endi


* If you are on the plant, grow!
	doif ov00 eq 1

		doif pose lt 3

			setv va00 pose

			addv va00 1

			pose va00

			stop

		else

* If you are on the plant and fully grown, become visible.

			bhvr 48

			attr 227

		endi

* If you are off the plant, and not falling or being carried by a creature or the hand
	elif ov00 eq 0

		doif carr eq pntr or crea carr eq 1 or fall eq 1

			stop

		endi

* And it is not night or dawn
		doif time ne 4 and time ne 0

* become invisible and break down
			lock

			attr 211

*play (very slow) decay anim

			anim [0 1 2 3 4]

			over

			wait 75

			gsub check

			unlk

		endi

	endi



	subr check

		setv va20 grap posx posy



		setv va21 prop va20 1

		setv va22 prop va20 2

		setv va23 prop va20 3

		setv va24 prop va20 4

		setv va26 rtyp va20



		doif va26 ge 8 and va26 le 9

			kill targ

		endi


		inst

		esee 2 4 50200
*don't factor old dying plants in the population check pls -t1ws
			doif targ ne null and ov10 lt 40

				addv va25 1

			endi

		next

		slow

		targ ownr

		doif va25 lt 4

			doif va21 gt .302157 and va22 ge .254510 and va23 ge .254510 and va24 ge .254510

				inst

				gsub grow

			else

				kill targ

			endi

		else

			kill targ

		endi



	retn



	subr grow

		setv va00 posl

		setv va01 post

		new: simp 2 4 50200 "moe_C2toDS_toms" 15 0 rand 1000 3000

		mvsf va00 va01

		kill ownr

	retn



endm


*Tomato Eat Script

scrp 2 8 50200 12

	inst

	sndc "chwp"


	stim writ from 78 1

*	wait 1
*
*	targ from
*
**chem 3 0.5
*
**chem 5 0.5
*
**chem 12 0.5
*
**	chem 149 -.25
*
*	wait 1
*
*	targ ownr

* to ensure creatures don't eat these to extinction, make them invisible after
* taking a bite and tell them to go directly to seed.
	doif base ne 17
		base 17
		pose 0

*check if there's any tomatoes in range
		esee 2 8 50200
			setv va00 1
		next

*if you're the last tomato in the area, become invisible to creatures and instantly seed
		doif va00 = 0

*force a drop on FROM if you're being carried by it (only if its a creature)
			doif from = carr and crea from = 1
				wait 4
				inst
				targ from
				drop
				targ ownr
			endi

			attr 211
			emit 6 0
			tick 10
		endi
	else
		kill ownr
	endi

endm



*scrp 2 8 50200 1
*
**mesg writ targ 12
*
*endm


*Tomato Collision Script

scrp 2 8 50200 6

	sndc "dr12"

endm




















*Tomato Machine Push Script

scrp 2 23 50210 1

	inst

*let the creature know they activated a machine
	stim writ from 90 1

*hand can't click it while it's in use!
	clac -1

*Send a signal from the specified output port to all connected inputs. 
	prt: send 0 _p1_

	snde "c2v1"
	setv va00 posl
	setv va01 posy

*16 was placing it inside a wall and making it freak out if it was too close to a wall. we sub the 6 later if TMVT is true.
*	subv va00 16
	subv va00 10

	subv va01 9

	anim [0 1 2 3 4 5]
	over

*make tomatoes and set some necessary params to stop them being weird
	inst
	new: simp 2 8 50200 "moe_C2toDS_toms" 23 15 rand 1000 2000

	base 12

	pose 0

	mira rand 0 1

	setv ov00 0

	setv ov02 1

	bhvr 48

	slow

*place it to the left of the ejection port if possible
	setv va02 va00
	subv va02 6
	doif tmvt va02 va01 = 1
		mvto va02 va01
	else
		mvsf va00 va01
	endi


*apply random velocity
	velo rand -3 -1 -5

	targ ownr
	anim [6 0]

*hand can click it now it's done animating.
	clac 0

endm



*Tomato Machine Pull Script

scrp 2 23 50210 2

*let the creature know they activated a machine
	stim writ from 90 1

*(activate 2 is the same as activate 1)
	mesg writ ownr 0
endm



*Tomato Machine Input Port Script

scrp 2 23 50210 1000
*if the signal strength wasn't 0
	doif _p1_ <> 0
*run the activate 1 script, but pass the Parameter the Input object sent on.
		mesg wrt+ ownr 0 _p1_ 0 0
	endi
endm



*Tomato Machine Hit Script

scrp 2 23 50210 3
	sndc "hit_"
	velo 0 rand -10 -20

*chance of disconnecting ports when a creature hits it (nahhh...)
*prt: bang rand 40 80

*let the creature know they hit a machine
	stim writ from 92 1
endm



**(disabled for now because it interrupts other scripts too frequently)
**Tomato Machine Collision Script
*
*scrp 2 23 50210 6
*
*	inst
*
*	snde "drp6"
*
**make sure hand can click it because this script will interrupt the activate script
*	clac 0
*
**if you got interrupted while trying to make a tomato, just try again
*	doif pose <> 0
*		mesg writ ownr 0
*	endi
*
*endm














*Remove

rscr


enum 2 4 50200

	kill targ

next

enum 2 8 50200

	kill targ

next

enum 2 23 50210
	kill targ
next


scrx 2 4 50200 10
scrx 2 4 50200 9

scrx 2 8 50200 10
scrx 2 8 50200 4
scrx 2 8 50200 1001
scrx 2 8 50200 9
scrx 2 8 50200 12
scrx 2 8 50200 1
scrx 2 8 50200 6


scrx 2 23 50210 1
scrx 2 23 50210 2
scrx 2 23 50210 1000
scrx 2 23 50210 3
scrx 2 23 50210 6
