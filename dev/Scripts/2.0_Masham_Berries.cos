*//*Masham Berry*//*
* 2 6 50212 - Masham Berry Leaf (invisible timer object that makes the berries)
* 2 8 50212 - Masham Berry (they look like coconuts are on the right side of the island)


*Matthew, "Moe", '10*
*The one who screams removed empty hunger decrease and made berries invisible until picked.
*NeoDement fixed plane issues
* (if you worked on this file, feel free to add your name!)


new: simp 2 6 50212 "moe_C2toDS_berries" 0 0 5000

attr 16

mvto 2190 48000

tick 1


scrp 2 6 50212 9



	tick rand 100 200



	doif totl 2 8 50212 lt 10

		setv va01 rand 2100 2139

		setv va02 rand 48080 48130

		gsub make_masham_berries

	endi



	subr make_masham_berries

		new: simp 2 8 50212 "moe_C2toDS_berries" 0 1 rand 3500 5000

		perm 55

		bhvr 48

		mvsf va01 va02

		accg 0

		attr 211

		elas 10

		fric 50

		tick rand 50 100

	retn

endm



*scrp 2 8 50212 10
*
**emit 6 .15
*
*endm



scrp 2 8 50212 9

	doif carr eq null

		addv ov00 1

		doif ov00 ge rand 20 40 and accg ne 6

			accg 6

			attr 195

			emit 6 .15

			velo rand -5 5 0

			setv ov00 0

		endi



		doif ov00 ge rand 10 20 and accg eq 6

			kill targ

		endi

	endi

endm



scrp 2 8 50212 1

	mesg writ targ 12

endm



*scrp 2 8 50212 4
*
** just gonna set the plane when we create it instead
*	plne rand 4000 6000
*
*endm



scrp 2 8 50212 5

	doif accg ne 6
		setv ov00 0
	endi
	accg 6

	attr 195

	emit 6 .15

endm

scrp 2 8 50212 6

	sndc "dr12"

endm



scrp 2 8 50212 12

	sndc "chwp"

	stim writ from 78 1

*	targ from

*Hunger for Fat *

*chem 151 -.31

*Hunger for Starch *

*chem 150 -.39

*Hunger for Protein *

*chem 149 -.27

*Fat* 80

*	chem 10 .31

*Starch 100*

*	chem 5 .39

*Protein 70*

*	chem 12 .27

*	targ ownr

	wait 1

	kill targ

endm

rscr

enum 2 6 50212

	kill targ

next

enum 2 8 50212

	kill targ

next

scrx 2 6 50212 9

scrx 2 8 50212 9

scrx 2 8 50212 6

scrx 2 8 50212 5

scrx 2 8 50212 12