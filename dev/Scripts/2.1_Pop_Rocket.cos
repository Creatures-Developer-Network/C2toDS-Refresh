* 2 21 50215 - Pop Rocket
* 1 23 50215 - Pop Rocket Pop

* Converted by NeoDement, has a few nice features the C2 one doesn't :)



inst



new: simp 2 21 50215 "moe_C2toDS_PopRocket" 19 0 5000

*this is where it appears in C2:
*random X coord, but to the general left of the incubator
*setv va00 rand 6852 7020
*mvto va00 48338

*but I think it would be nicer in the volcano by default, the incubator room is already pretty crowded in C2toDS. Plus the ettin and grendel could always use more toys!
mvto 9967 49705


*creation script

scrp 2 21 50215 10

*C2 framerate
	frat 2

	attr 199
	bhvr 43
	accg 2
	aero 5

	elas 0
	fric 100

*perm 51 means it can't fall through wooden platforms
	perm 51


endm



*activate 1 script

scrp 2 21 50215 1

*played with a toy stim
	stim writ from 97 1

*launch rocket!
	call 1001 from 0

endm



*activate 2 script

scrp 2 21 50215 2

*played with a toy stim
	stim writ from 97 1

*launch rocket!
	call 1001 from 0

endm



*hit script

scrp 2 21 50215 3

*played with a toy stim
	stim writ from 97 1

*check if the rocket is in the unlaunched pose.
	doif base = 0

*face either left or right and enter the crashed pose.
		setv va00 rand 0 1

		doif va00 = 0
			base 1
		else
			base 10
		endi

		pose 8
		setv ov99 0
	endi

	velo rand -5 5 rand -6 -2

endm




*launch rocket script

*_P1_ is whoever called the script

scrp 2 21 50215 1001

	inst

*reset ov97 just in case...
	setv ov97 0

*before you launch, check if you're in the correct base (0 is used for not launched) and NOT being held by the hand
	doif base = 0 and carr <> pntr


*force it to be dropped if a creature is holding it
		doif crea carr = 1
*set a reminder that a creature just dropped you. if this variable is true when the drop script runs, then it runs the activate script again instead.
			setv ov97 1
			targ carr
			drop
			targ ownr
		endi

*ov99 is set to 1 when a rocket launches and back to 0 when it lands. it's used for locking scripts out.
		setv ov99 1

		setv va00 rand 0 1

		setv vely -40

*temporarily switching to perm 50 so it can go through wooden platforms on the way up
*(should be cool for the lift shaft)

*(only do this if you were activated by the pointer. otherwise creatures might launch it out of their own reach by mistake.
		doif _p1_ = pntr
			perm 50
		endi

		doif va00 eq 0
			setv velx -10
			base 1
		else
			setv velx 10
			base 10
		endi

*in C2 these frames were doubled, but that didn't make sense with the double physics framerate of C3/DS
		anim [0 1 2 3 4 5 6 7]
		over

*down we go!

		inst

*if it was sent up by the pointer it needs to try to reset the perm
		doif _p1_ = pntr
*start the timer ticking, to try and set perm back to 51 as soon as possible
			tick 1
		endi

		doif va00 eq 0
			setv velx -2
		else
			setv velx 2
		endi

*otherwise do a lil jump and reset all your variables if you are activated while in crashed state or in default state
*this prevents you from activating the rocket again while it's in mid-air

	elif base = 0 or pose = 8
		base 0
		pose 0
		setv ov99 0
		velo rand -2 2 -3
	endi

endm



*timer script

scrp 2 21 50215 9

	inst

	setv va50 perm

*try and set perm to 51 if it's not inside a wall
*temporarily disable our hitbox, change perm, then test if we can have our old hitbox.
*if not, change back to old perm and old hitbox.

	core 0 0 0 0

	perm 51
	doif tcor 0 hght wdth 0 = 0
		perm va50
	else
*stop ticking if we set our perm successfully
		tick 0
	endi
	core 0 hght wdth 0

endm



*pick up script

scrp 2 21 50215 4

	inst

*manually reset perm
	tick 0
	perm 51

*reset variables
	base 0
	pose 0
	setv ov99 0

endm


*drop script

scrp 2 21 50215 5

*if ov97 is true, it means a creature was just forced to drop it because they activated it while it was in their hand.
	doif ov97 = 0
		inst

*start the timer ticking, to try and set perm back to 51 as soon as possible
		tick 1

	else

*launch rocket!
		call 1001 from 0
	endi

endm



*collision script

scrp 2 21 50215 6


*try and reset perm when possible
	tick 1


	lock
*disable picking it up or anything else while it's locked
	attr 0

	gsub cpop
	doif ov99 eq 1
		doif va09 eq 1
			pose 8
			setv va00 posl
			subv va00 8
			setv va01 post

			inst

*variable to let the rocket know it just exploded and shouldn't make a collision sound
			setv ov98 1

			new: simp 1 23 50215 "moe_C2toDS_PopRocket" 5 19 5001
*C2 framerate
			frat 2
			mvto va00 va01
			snde "popr"
			base 0
			anim [1 2 3 4]
			slow
			over
			inst
			kill targ

*Some stuff for when nearby creatures see it pop
			esee 4 0 0

*				boredom -0.5
				chem 159 -0.5

*the original script from C2 also made them a bit scared and uncomfortable whenever the rocket landed.
*which to me, seemed like an unintuitive thing for a toy to do...
*				fear +0.1
*				chem 158 0.1
*				comfort -0.1
*				chem 162 -0.1

			next

		else
			pose 8

*only make a sound when hitting the floor and do NOT make the sound if you just exploded.
			doif wall ne _up_ and ov98 ne 1
				setv ov98 0
				snde "drp6"
			endi
		endi
	else
*only make a sound when hitting the floor
		doif wall ne _up_
			snde "drp6"
		endi
	endi
	unlk
*re-enable picking it up and etc now it's unlocked
	attr 199

	subr cpop
		setv va09 0
		doif pose eq 7
			doif wall eq down
				setv va09 1
			endi
		endi
	retn
endm







*goodbye

rscr

scrx 2 21 50215 10
scrx 2 21 50215 1
scrx 2 21 50215 2
scrx 2 21 50215 3
scrx 2 21 50215 1001
scrx 2 21 50215 9
scrx 2 21 50215 4
scrx 2 21 50215 5
scrx 2 21 50215 6

enum 2 21 50215
	kill targ
next


scrx 1 23 50215 9

enum 1 23 50215
	kill targ
next