*//*Cantharis Berry*//*

* 2 6 50207 - Cantharis Berry Leaf
* 2 8 50207 - Cantharis Berry

*Matthew, "Moe", '10*
*The one who screams removed empty hunger decrease, made berries invisible until picked, and shortened lifespan.
*NeoDement fixed berries appearing on the beach above the plant instead of on the plant + fixed plane issues

new: simp 2 6 50207 "moe_C2toDS_berries" 0 0 5000

attr 16

mvto 10340 48376

tick 1

scrp 2 6 50207 9



	tick rand 100 200



	doif totl 2 8 50207 lt 15

*make a new berry
		inst
		new: simp 2 8 50207 "moe_C2toDS_berries" 0 8  rand 3500 5000

*sometimes mvsf puts berries on the beach, so keep randomizing the coords until a safe position ON THE PLANT is found
		loop
			setv va00 rand 10223 10450

			setv va01 rand 48340 48470

		untl tmvt va00 va01 = 1

		mvto va00 va01

	endi

endm



scrp 2 8 50207 10

	inst

	perm rand 45 55

	bhvr 48

	accg 0

	attr 211

	elas 10

	fric 50

	tick rand 50 100

*emit 6 .15

endm



scrp 2 8 50207 9

	doif carr eq null

		addv ov00 1

		doif ov00 ge rand 20 40 and accg ne 4

			accg 4

			attr 195

			emit 6 .15

			velo rand -5 5 0

			setv ov00 0

		endi



		doif ov00 ge rand 10 20 and accg eq 4

			kill targ

		endi

	endi

endm



*scrp 2 8 50207 1
*
*	mesg writ targ 12
*
*endm
*
*
*
*scrp 2 8 50207 4
*
* just gonna set the plane when we create it instead
*	plne rand 4000 6000
*
*endm



scrp 2 8 50207 5

	doif accg ne 4

		setv ov00 0

	endi

	accg 4

	attr 195

	emit 6 .15

endm



scrp 2 8 50207 6

	sndc "dr12"

endm





scrp 2 8 50207 12

	sndc "chwp"

	stim writ from 78 1

	targ from

*Hunger for Fat 50*

*chem 151 -.23

*Hunger for Starch 50*

*chem 150 -.19

*Hunger for Protein 50*

*chem 149 -.23

*Fat* 60

*	chem 10 .23

*Starch 50*

*	chem 5 .19

*Protein 60*

*	chem 12 .23

*EDTA

	chem 95 .35

*Sodium Thisulphite

	chem 96 .35

*Histamine A

	chem 73 .35

	targ ownr

	wait 1

	kill targ

endm

rscr

enum 2 6 50207

	kill targ

next

enum 2 8 50207

	kill targ

next

scrx 2 6 50207 9

scrx 2 8 50207 9

scrx 2 8 50207 6

scrx 2 8 50207 5

scrx 2 8 50207 12