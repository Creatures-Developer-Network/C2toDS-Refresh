* 1 8 50202 - Some decorative bits related to the trolly
* 3 1 50205 - Trolly

* The call buttons still aren't consistent with how they light up
* But I'm tired and it's a very minor issue that can be fixed on a more detailed pass x.x


*left pulley
new: simp 1 8 50202 "moe_C2toDS_trolly" 1 5 3
attr 16
mvto 4457 48574


*right pulley
new: simp 1 8 50202 "moe_C2toDS_trolly" 1 6 3
attr 16
mvto 5878 48235


*cable
new: simp 1 8 50202 "moe_C2toDS_trolly" 1 7 2
attr 16
mvto 4483 48265







*C1 Trolly, Found in the wilderness. :D



new: vhcl 3 1 50205 "moe_C2toDS_trolly" 1 0 2000

mvsf 5800 48260

*mvsf 4582 48915
cato 35
attr 16
setv ov00 5

*Left

pat: butt 1 "moe_C2toDS_trolly" 1 2 18 58 0 [] 1001 0

*Right

pat: butt 2 "moe_C2toDS_trolly" 3 2 81 58 0 [] 1002 0

*Handle

pat: dull 3 "moe_C2toDS_trolly" 8 36 125 -1



cabn 0 0 wdth hght

cabp -10

cabw 10

new: simp 3 12 50205 "moe_C2toDS_lifts" 2 7 1999

attr 4

bhvr 3
* Left Butt
setv ov00 5

cato 2

mvto 4523 48825

new: simp 3 12 50205 "moe_C2toDS_lifts" 2 7 1999

attr 4

bhvr 3
* Right Butt
setv ov00 -5

cato 2

mvto 5879 48340

**Call Button scripts
scrp 3 12 50205 1
	inst
* light up, light up
	rtar 3 1 50205
	doif ov00 eq mv00
		targ ownr
		pose 1
	endi
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va48 gtos 0
* check for any existing queue object
		enum 3 13 50205
			doif ov01 eq va48
				addv va49 1
			endi
		next
* make a queue object if there's not one already
		doif va49 eq 0
			new: simp 3 13 50205 "blnk" 1 0 0
*dbg: outs "making quere"
			setv ov00 mv00
			sets ov01 va48
			tick 20
		endi

		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
	endi
	rtar 3 1 50205
	mesg writ targ 0
endm
**Call Button scripts
scrp 3 12 50205 2
	inst
* light up, light up
	rtar 3 1 50205
	doif ov00 eq mv00
		targ ownr
		pose 1
	endi
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va48 gtos 0
* check for any existing queue object
		enum 3 13 50205
			doif ov01 eq va48
				addv va49 1
			endi
		next
* make a queue object if there's not one already
		doif va49 eq 0
			new: simp 3 13 50205 "blnk" 1 0 0
*dbg: outs "making quere"
			setv ov00 mv00
			sets ov01 va48
			tick 20
		endi

		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
	endi
	rtar 3 1 50205
	mesg writ targ 0
endm

scrp 3 13 50205 9
	inst
* Just here to continue injecting wait
	doif ooww ov01 eq 3
		targ mtoc ov01
		doif targ ne null
			doif carr eq null
				stim writ from 75 1
			else
* Picked up, stop queueing
				stim writ targ 96 0
				driv 19 -1.0
				kill ownr
			endi
		else
			kill ownr
		endi
	else
		kill ownr
	endi
endm

**For later
scrp 3 1 50205 1
*
*	stim writ from 94 1

*	stim writ from 75 1
	doif velx eq 0
		doif posl gt 4530
* go left - will setv ov00 -5

			mesg writ ownr 1001

		else
* go right - will setv ov00 5

			mesg writ ownr 1002

		endi
	endi
endm



scrp 3 1 50205 1001
	lock
	tick 2



	setv ov00 -5



*setv ov01 1.9

	setv ov01 1.8



	part 1

	pose 1



	part 2

	pose 0



	part 3

	pose 1


	doif velx eq 0
		inst
* Get the queued creatures
		enum 3 13 50205
			doif targ ne null
				doif ov00 eq -5
					doif ooww ov01 eq 3
						tick 0
* spas takes some time to work, so loop
* and keep checking if you got a creature
* before continuing
						loop
							spas ownr mtoc ov01
							wait 1
							addv va46 1
							seta va45 targ
							targ ownr
							epas 4 0 0
								addv va44 1
							next
							targ va45
						untl va44 gt 0 or ooww ov01 ne 3
	
						kill targ
					else
						kill targ
					endi
				endi
			endi
		next
		slow
	endi

	unlk
endm



scrp 3 1 50205 1002
	lock
	tick 2



	setv ov00 5



*setv ov01 -1.9

	setv ov01 -1.8



	part 2

	pose 1



	part 1

	pose 0



	part 3

	pose 2


	doif velx eq 0
		inst
* Get the queued creatures
		enum 3 13 50205
			doif targ ne null
				doif ov00 eq 5
					doif ooww ov01 eq 3
						tick 0
* spas takes some time to work, so loop
* and keep checking if you got a creature
* before continuing
						loop
							spas ownr mtoc ov01
							wait 1
							addv va46 1
							seta va45 targ
							targ ownr
							epas 4 0 0
								addv va44 1
							next
							targ va45
						untl va44 gt 0 or ooww ov01 ne 3

						kill targ
					else
						kill targ
					endi
				endi
			endi
		next
		slow
	endi
	unlk
endm


*Trolly Timer
scrp 3 1 50205 9
	lock
	doif ov00 lt 0
		inst
		epas 4 0 0
			stim writ from 94 0
			stim writ from 75 1
		next
		slow
		doif posl gt 4500

			setv velx ov00

			setv vely ov01

		else
			inst
			setv velx 0

			setv vely 0

			part 1

			pose 0

			part 3

			pose 0
			epas 4 0 0
				driv 19 -1.0
			next
			dpas 4 0 0
			unlk
			mvsf 4498 48725
			enum 3 12 50205
				pose 0
			next
			doif totl 3 13 50205 gt 0
				mesg writ ownr 0
			endi
		endi

	elif ov00 gt 0
		inst
		epas 4 0 0
			stim writ from 94 0
			stim writ from 75 1
		next
		slow
		doif posl lt 5800

			setv velx ov00

			setv vely ov01

		else
			inst
			setv velx 0

			setv vely 0

			part 2

			pose 0

			part 3

			pose 0
			epas 4 0 0
				driv 19 -1.0
			next
			dpas 4 0 0
			unlk
			mvsf 5800 48260
			enum 3 12 50205
				pose 0
			next
			doif totl 3 13 50205 gt 0
				mesg writ ownr 0
			endi
		endi
		unlk
	endi
endm


rscr

enum 3 1 50205

	kill targ

next

enum 1 8 50202

	kill targ

next
enum 3 12 50205

	kill targ

next
enum 3 13 50205

	kill targ

next