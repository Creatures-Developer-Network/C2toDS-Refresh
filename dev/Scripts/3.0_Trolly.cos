* 1 8 50202 - Some decorative bit related to the trolly
* 3 1 50205 - Trolly

new: simp 1 8 50202 "moe_C2toDS_trolly" 1 5 2
attr 16
mvto 4457 48574



new: simp 1 8 50202 "moe_C2toDS_trolly" 1 6 8002
attr 16
mvto 5878 48235



new: simp 1 8 50202 "moe_C2toDS_trolly" 1 7 1
attr 16
mvto 4483 48265







*C1 Trolly, Found in the wilderness. :D



new: vhcl 3 1 50205 "moe_C2toDS_trolly" 1 0 2000

mvsf 5800 48260

*mvsf 4582 48915
cato 35
attr 16

*Left

pat: butt 1 "moe_C2toDS_trolly" 1 2 18 58 0 [] 1001 0

*Right

pat: butt 2 "moe_C2toDS_trolly" 3 2 81 58 0 [] 1002 0

*Handle

pat: dull 3 "moe_C2toDS_trolly" 8 36 125 -1



cabn 0 0 wdth hght

cabp -10

cabw 10

new: simp 3 12 50205 "blnk" 1 1 2000

attr 0

bhvr 3
* Left Butt
setv ov00 0

cato 2

mvto 4523 48863

new: simp 3 12 50205 "blnk" 1 1 2000

attr 0

bhvr 3
* Right Butt
setv ov00 1

cato 2

mvto 5879 48388

**Call Button scripts
scrp 3 12 50205 1
	inst
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va00 gtos 0
		new: simp 3 13 50205 "blnk" 1 0 0
		setv ov00 mv00
		sets ov01 va00
		tick 20
		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
		rtar 3 1 50205
		mesg writ targ 0
	endi
endm
**Call Button scripts
scrp 3 12 50205 2
	inst
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va00 gtos 0
		new: simp 3 13 50205 "blnk" 1 0 0
		setv ov00 mv00
		sets ov01 va00
		tick 20
		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
		rtar 3 1 50205
		mesg writ targ 0
	endi
endm

scrp 3 13 50203 9
	inst
* Just here to continue injecting wait
	doif ooww ov01 eq 3
		targ mtoc ov01
		doif targ ne null
			stim writ from 75 1
		else
			kill ownr
		endi
	else
		kill ownr
	endi
endm

**For later
scrp 3 1 50205 1
*
*	stim writ from 94 1

*	stim writ from 75 1
	doif velx eq 0
		doif posl gt 4530

			mesg writ ownr 1001

		else

			mesg writ ownr 1002

		endi
	endi
endm



scrp 3 1 50205 1001
	lock
	tick 2



	setv ov00 -5



*setv ov01 1.9

	setv ov01 1.8



	part 1

	pose 1



	part 2

	pose 0



	part 3

	pose 1


doif velx eq 0
	inst
* Get the queued creatures
	enum 3 13 50205
		doif ov00 eq 1
			doif ooww ov01 eq 3
				spas ownr mtoc ov01
				kill targ
			else
				kill targ
			endi
		endi
	next
	slow
endi

	unlk
endm



scrp 3 1 50205 1002
	lock
	tick 2



	setv ov00 5



*setv ov01 -1.9

	setv ov01 -1.8



	part 2

	pose 1



	part 1

	pose 0



	part 3

	pose 2


doif velx eq 0
	inst
* Get the queued creatures
	enum 3 13 50205
		doif ov00 eq 0
			doif ooww ov01 eq 3
				spas ownr mtoc ov01
				kill targ
			else
				kill targ
			endi
		endi
	next
	slow
endi

	unlk
endm


*Trolly Timer
scrp 3 1 50205 9
	lock
	doif ov00 lt 0
		inst
		epas 4 0 0
			stim writ from 94 0
			stim writ from 75 1
		next
		slow
		doif posl gt 4500

			setv velx ov00

			setv vely ov01

		else
			inst
			setv velx 0

			setv vely 0

			part 1

			pose 0

			part 3

			pose 0
			epas 4 0 0
				driv 19 -1.0
			next
			dpas 4 0 0
			unlk
			mvsf 4498 48725
			doif totl 3 13 50205 gt 0
				mesg writ ownr 0
			endi
		endi

	elif ov00 gt 0
		inst
		epas 4 0 0
			stim writ from 94 0
			stim writ from 75 1
		next
		slow
		doif posl lt 5800

			setv velx ov00

			setv vely ov01

		else
			inst
			setv velx 0

			setv vely 0

			part 2

			pose 0

			part 3

			pose 0
			epas 4 0 0
				driv 19 -1.0
			next
			dpas 4 0 0
			unlk
			mvsf 5800 48260
			doif totl 3 13 50205 gt 0
				mesg writ ownr 0
			endi
		endi
		unlk
	endi
endm


rscr

enum 3 1 50205

	kill targ

next

enum 1 8 50202

	kill targ

next
enum 3 12 50205

	kill targ

next
enum 3 13 50205

	kill targ

next
