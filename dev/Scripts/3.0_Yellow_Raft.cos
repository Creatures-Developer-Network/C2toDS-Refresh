* 3 1 50203 - Yellow Raft
* 3 12 50203 - Call Buttons
* 3 13 50203 - Queue Agents

* Aiko changed the placeholder sprites to call buttons (and made them light up)
* Also made the creature-grabbing more reliable

* UNFINISHED: Need to add code to detect which direction the norn wants to go in, rather than
*             relying on where they are in relation to the dingy when they activated it.
* UNFINISHED?: We also need to ensure that norns falling off ledge are caught by dingy. I 
* 		  thought setting greedy cabin would do this, but apparently not.
* Note: Might be working better due to Aiko's changes - Verm

* Rubber dingy

new: vhcl 3 1 50203 "moe_C2toDS_dingy" 22 0 3000

tick 4

cabn 60 -50 225 88

cabp 1

pat: dull 1 "moe_C2toDS_dingy" 1 0 0 4

pat: dull 10 "moe_C2toDS_dingy" 2 15 65 2

pat: dull 11 "moe_C2toDS_dingy" 12 230 65 2
* doing the same treatment here as the boat got - Verm
attr 20

*bhvr 3

setv ov00 1

mvto 2380 48195

setv ov99 posy

cato 35

new: simp 3 12 50203 "moe_C2toDS_lifts" 2 7 2000

attr 4

bhvr 3
* Left Butt
setv ov00 0

cato 2

mvto 2395 48128

new: simp 3 12 50203 "moe_C2toDS_lifts" 2 7 2000

attr 4

bhvr 3
* Right Butt
setv ov00 1

cato 2

mvto 3085 48128

scrp 3 12 50203 1
	inst
	* light up, light up
	rtar 3 1 50203
	doif ov00 eq mv00
		targ ownr
		pose 1
	endi
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va48 gtos 0
		* check for any existing queue object
		enum 3 13 50203
			doif ov01 eq va48
				addv va49 1
			endi
		next
		* make a queue object if there's not one already
		doif va49 eq 0
			new: simp 3 13 50203 "blnk" 1 0 0
			*dbg: outs "making quere"
			setv ov00 mv00
			sets ov01 va48
			tick 20
		endi

		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
	endi
	rtar 3 1 50203
	mesg writ targ 1001
endm

scrp 3 12 50203 2
	inst
	* light up, light up
	rtar 3 1 50203
	doif ov00 eq mv00
		targ ownr
		pose 1
	endi
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va48 gtos 0
		* check for any existing queue object
		enum 3 13 50203
			doif ov01 eq va48
				addv va49 1
			endi
		next
		* make a queue object if there's not one already
		doif va49 eq 0
			new: simp 3 13 50203 "blnk" 1 0 0
			*dbg: outs "making quere"
			setv ov00 mv00
			sets ov01 va48
			tick 20
		endi

		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
	endi
	rtar 3 1 50203
	mesg writ targ 1001
endm

scrp 3 13 50203 9
	inst
* Just here to continue injecting wait
	doif ooww ov01 eq 3
		targ mtoc ov01
		doif targ ne null
			doif carr eq null
				stim writ from 75 1
			else
* Picked up, stop queueing
				stim writ targ 96 0
				driv 19 -1.0
				kill ownr
			endi
		else
			kill ownr
		endi
	else
		kill ownr
	endi
endm

scrp 3 1 50203 1
	inst
	mesg writ ownr 1001
endm

scrp 3 1 50203 1001

	lock
	*dbg: outs "1001!!!"
	loop
*Going left - right station
		doif ov00 eq 0
			inst
* Get the queued creatures
			enum 3 13 50203
				doif ov00 eq 1
					doif ooww ov01 eq 3
						
						* spas takes some time to work, so loop
						* and keep checking if you got a creature
						* before continuing
						loop
							spas ownr mtoc ov01
							wait 1
							addv va46 1
							seta va45 targ
							targ ownr
							epas 4 0 0
								addv va44 1
							next
							targ va45
						untl va44 gt 0 or ooww ov01 ne 3 
	
						kill targ
					else
						kill targ
					endi
				endi
			next
			slow

			part 11

			anim [0 1 2 3 4 5 6 7 8 9 255 3]

			sndl "dngy"

			setv va02 2

			loop

				velo -8 va02

				negv va02

				wait 2
				epas 4 0 0
					stim writ targ 75 1
				next

			untl posx le 2545

			stpc

			setv velx 0

			setv vely 0

			anim [2 1 0]

			targ ownr
			doif post lt 48195
				loop

					addv vely 0.5

					wait 1
					epas 4 0 0
						stim writ targ 75 1
					next

				untl post ge 48195
			endi
			setv vely 0

			inst
			epas 4 0 0
				rpas ownr targ
				mvft 2390 48219
				stim writ targ 96 0
				driv 19 -1.0
			next
			setv ov00 1
*Going right - left station
		elif ov00 eq 1
			inst
* Get the queued creatures
			enum 3 13 50203
				doif ov00 eq 0
					doif ooww ov01 eq 3
						
						* spas takes some time to work, so loop
						* and keep checking if you got a creature
						* before continuing
						loop
							spas ownr mtoc ov01
							wait 1
							addv va46 1
							seta va45 targ
							targ ownr
							epas 4 0 0
								addv va44 1
							next
							targ va45
						untl va44 gt 0 or ooww ov01 ne 3 
	
						kill targ
					else
						kill targ
					endi
				endi
			next
			slow

			part 10

			anim [0 1 2 3 4 5 6 7 8 9 255 3]

			sndl "dngy"

			setv va02 2

			loop

				velo 8 va02

				negv va02

				wait 2
				epas 4 0 0
					stim writ targ 75 1
				next
			untl posx ge 2950

			stpc

			setv velx 0

			setv vely 0

			anim [2 1 0]

			targ ownr
			doif post gt 48195
				loop
					subv vely 0.5

					wait 1
					epas 4 0 0
						stim writ targ 75 1
					next
				untl post le 48195
			endi
			setv vely 0

			inst
			epas 4 0 0
				rpas ownr targ
				mvft 3103 48219
				stim writ targ 96 0
				driv 19 -1.0
			next
			setv ov00 0
		endi
		targ ownr
	untl totl 3 13 50203 le 0
	
	enum 3 12 50203
		pose 0
	next
	unlk
	

endm

* pickup creatures who fall in you
scrp 3 1 50203 9
	inst
	targ ownr
	etch 4 0 0
		doif posb ge mv99
			*targ ownr
			spas ownr targ
			*gpas 4 0 0 0
		endi
	next
	
	epas 4 0 0
		addv va00 1
	next
	
	doif va00 gt 0
		mesg writ ownr 1001
		wait 1
	endi

endm

rscr

enum 3 1 50203

	kill targ

next

enum 3 12 50203

	kill targ

next

enum 3 13 50203

	kill targ

next

scrx 3 1 50203 1

scrx 3 1 50203 2

scrx 3 1 50203 3
