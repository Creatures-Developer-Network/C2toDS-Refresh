* 3 1 50203 - Yellow Raft
* 3 12 50203 - Call Buttons
* 3 13 50203 - Queue Agents

* UNFINISHED: Need to add code to detect which direction the norn wants to go in, rather than
*             relying on where they are in relation to the dingy when they activated it.
* UNFINISHED: We also need to ensure that norns falling off ledge are caught by dingy. I 
* 		  thought setting greedy cabin would do this, but apparently not.

* Rubber dingy

new: vhcl 3 1 50203 "moe_C2toDS_dingy" 22 0 3000

cabn 60 -50 225 88

cabp 1

pat: dull 1 "moe_C2toDS_dingy" 1 0 0 4

pat: dull 10 "moe_C2toDS_dingy" 2 15 65 2

pat: dull 11 "moe_C2toDS_dingy" 12 230 65 2
* doing the same treatment here as the boat got - Verm
attr 20

*bhvr 3

setv ov00 1

mvsf 2380 48200

cato 35

new: simp 3 12 50203 "blnk" 1 1 2000

attr 0

bhvr 3
* Left Butt
setv ov00 0

cato 2

mvto 2395 48170

new: simp 3 12 50203 "blnk" 1 1 2000

attr 0

bhvr 3
* Right Butt
setv ov00 1

cato 2

mvto 3085 48170

scrp 3 12 50203 1
	inst
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va00 gtos 0
		new: simp 3 13 50203 "blnk" 1 0 0
		setv ov00 mv00
		sets ov01 va00
		tick 20
		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
		rtar 3 1 50203
		mesg writ targ 1001
	endi
endm

scrp 3 12 50203 2
	inst
*Queue Object!
	targ from
	doif crea targ eq 1
		sets va00 gtos 0
		new: simp 3 13 50203 "blnk" 1 0 0
		setv ov00 mv00
		sets ov01 va00
		tick 20
		stim writ from 75 1
		stim writ from 96 1
		urge writ from 0 0.5 0 0.5
		rtar 3 1 50203
		mesg writ targ 1001
	endi
endm

scrp 3 13 50203 9
	inst
* Just here to continue injecting wait
	doif ooww ov01 eq 3
		targ mtoc ov01
		doif targ ne null
			stim writ from 75 1
		else
			kill ownr
		endi
	else
		kill ownr
	endi
endm

scrp 3 1 50203 1
	inst
	mesg writ ownr 1001
endm

scrp 3 1 50203 1001

	lock
	loop
*Going left - right station
		doif ov00 eq 0
			inst
* Get the queued creatures
			enum 3 13 50203
				doif ov00 eq 1
					doif ooww ov01 eq 3
						spas ownr mtoc ov01
						kill targ
					else
						kill targ
					endi
				endi
			next
			slow

			part 11

			anim [0 1 2 3 4 5 6 7 8 9 255 3]

			sndl "dngy"

			setv va02 2

			loop

				velo -8 va02

				negv va02

				wait 2
				epas 4 0 0
					stim writ targ 75 1
				next

			untl posx le 2545

			stpc

			setv velx 0

			setv vely 0

			anim [2 1 0]

			targ ownr

			loop

				addv vely -1

				wait 1
				epas 4 0 0
					stim writ targ 75 1
				next

			untl posy ge 48200

			setv vely 0

			inst
			epas 4 0 0
				rpas ownr targ
				mvft 2390 48219
				stim writ targ 96 0
				driv 19 -1.0
			next
			setv ov00 1
*Going right - left station
		elif ov00 eq 1
			inst
* Get the queued creatures
			enum 3 13 50203
				doif ov00 eq 0
					doif ooww ov01 eq 3
						spas ownr mtoc ov01
						kill targ
					else
						kill targ
					endi
				endi
			next
			slow

			part 10

			anim [0 1 2 3 4 5 6 7 8 9 255 3]

			sndl "dngy"

			setv va02 2

			loop

				velo 8 va02

				negv va02

				wait 2
				epas 4 0 0
					stim writ targ 75 1
				next
			untl posx ge 2950

			stpc

			setv velx 0

			setv vely 0

			anim [2 1 0]

			targ ownr

			loop

				addv vely -1

				wait 1
				epas 4 0 0
					stim writ targ 75 1
				next
			untl posy ge 48200

			setv vely 0

			inst
			epas 4 0 0
				rpas ownr targ
				mvft 3103 48219
				stim writ targ 96 0
				driv 19 -1.0
			next
			setv ov00 0
		endi
		targ ownr
	untl totl 3 13 50203 le 0
	unlk

endm

rscr

enum 3 1 50203

	kill targ

next

enum 3 12 50203

	kill targ

next

enum 3 13 50203

	kill targ

next

scrx 3 1 50203 1

scrx 3 1 50203 2

scrx 3 1 50203 3
