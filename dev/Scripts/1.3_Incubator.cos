* 2 22 50200 - Incubator

* Core done, just some graphical adjustments needed

*Incubator is now properly placed - Moe
* Aiko fixed invalid targ errors by inst'ing the enuming of creatures



new: vhcl 2 22 50200 "moe_C2toDS_incubator" 26 0 20

cabn 53 35 135 111

cabp 21

pat: dull 10 "moe_C2toDS_incubator" 8 0 0 23

pat: dull 11 "moe_C2toDS_incubator" 16 69 44 22

attr 24

pose 7

frat 3

part 10

frat 3

pose 7

part 11

frat 3

tick 500

mvto 7043 48340


scrp 2 22 50200 9
	
	inst

	setv va98 game "breeding_limit"

	setv va99 0

	enum 4 0 0

		doif dead = 0

			addv va99 1

		endi

	next

	targ ownr

	doif va99 > va98 and ov00 ne 1

		setv ov00 1

		snde "incc"

		part 0

		wait 10

		part 0

		anim [ 7 6 5 4 3 2 1 0 ]

		part 10

		anim [ 7 6 5 4 3 2 1 0 ]

		attr 16

		tick 200

	elif va99 < va98 and ov00 ne 0

		snde "inco"

		part 0

		anim [ 0 1 2 3 4 5 6 7 ]

		part 10

		anim [ 0 1 2 3 4 5 6 7 ]

		over

		attr 24

		tick 1

		setv ov00 0

	endi





endm



scrp 2 22 50200 124

	doif ov00 eq 0

		epas 3 4 0

			doif targ ne null

				mesg wrt+ ownr 1001 255 0 0

			endi

		next

	endi

endm



scrp 2 22 50200 1001

	wait 5

	lock

	inst

	epas 3 4 0

** 		get egg agent

		seta va99 targ

		setv va01 ov01

		doif gtos 1 = "" and targ <> null

			kill targ

		elif gtos 1 <> "" and targ <> null

			targ ownr

			attr 16

			part 0

			anim [7 6 5 4 3 2 1 0]

			part 10

			anim [7 6 5 4 3 2 1 0]

			sndc "incc"

			over

			wait 20



			part 11

			anim [1 2 3 4 5 6 7 8 9 255]



			targ ownr

			part 0

			snde "incu"



			targ ownr

			doif va01 = 0 or va01 = 1 or va01 = 2

				new: crea 4 va99 1 va01 0

			else

				new: crea 4 va99 1 0 0

			endi

			accg game "c3_creature_accg"

			bhvr game "c3_creature_bhvr"

			attr game "c3_creature_attr"

			perm game "c3_creature_perm"

			setv va91 11

			addv va91 gnus

			emit va91 0.5

			drea 1

			seta va05 targ



			targ va99

			anim [4 5 6 4 5 6 4 5 6 3 3 6 4 5 6 4 4 5 6 3]

			snde "crak"

			over

			stpc

			wait 30

			anim [4 5 6 4 5 6 4 5 6 3 3 6 4 5 6 4 4 5 6 3]

			snde "crak"

			over

			stpc

			wait 40

			anim [4 5 4 5 4 5 6 5 3 3 4 5 6 6 5 6 5 6 5 6 5 3]

			snde "crak"

			over

			stpc

			wait 40

			anim [4 5 4 5 4 5 6 5 3 3 4 5 6 6 5 6 5 6 5 6 5 3]

			snde "crak"

			over

			stpc

			wait 20

			anim [4 5 6 4 5 6 4 5 6 5 6 5 6 4 5 6 4 5 6 6 6 5 6 5 6 5 6 5 4 5 6]

			snde "crak"

			setv va00 posl

			setv va01 posb

			over

			pose 7

			fade



			targ ownr

			part 11

			pose 0



			part 0

			snde "inco"

			anim [ 0 1 2 3 4 5 6 7 ]



			part 10

			anim [ 0 1 2 3 4 5 6 7 ]



			targ ownr

			part 0

			fade



			targ va05

			pose 75

			subv va01 10

			wait 20

			mvsf va00 va01

			aslp 0

			born

			over

			targ ownr

			attr 24

		endi

	next

	dpas 3 4 0

	slow

	unlk

	wait rand 20 30

	targ va99

	doif targ ne null

		kill targ

	endi

endm

rscr

enum 2 22 50200

	kill targ

next

scrx 2 22 50200 9
