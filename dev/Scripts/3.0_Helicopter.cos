* 3 2 50206 - Helicopter

* Aiko made this invisible to creatures for now 

* NeoDement:
* - Fixed heli being categorized as a Teleporter
* - Changed heli to pick up creatures as it takes flight instead of requiring a seperate press on the body of the vehicle.
* - Added some chemical reductions to make it fun for creatures to ride
* - Fixed an issue where the heli ACCG would end up way too high after flying once 


new: vhcl 3 2 50206 "moe_C2toDS_sola" 14 2 1000

mvsf 6480 47986




*creation script

scrp 3 2 50206 10


*override the category to Vehicle instead of Teleporter
	cato 35


	pat: butt 1 "moe_C2toDS_sola" 32 2 180 48 0 [] 1000 0

	pat: butt 2 "moe_C2toDS_sola" 34 2 257 175 0 [] 1001 0

	pat: butt 3 "moe_C2toDS_sola" 36 2 102 176 0 [] 1002 0


*C2 framerate

	frat 2
	part 1
	frat 2
	part 2
	frat 2
	part 3
	frat 2
	part 0


*pat: dull 4 "moe_C2toDS_sola" 0 0 0 0


	cabw 10

	cabp -10

	cabn 125 75 270 200


*Carryable + Mouseable + Activateable + Invisible + Suffer Collisions + Suffer Physics
	attr 215

	elas 0


*TODO: perm 51 stops it falling through the bridge and volcano
*... but it also stops it spawning, so I guess we gotta edit the map...
*	perm 51

	perm 50

*accg value (default)
	setv ov05 2
	accg ov05

*accg value (during landing? not sure)
	setv ov03 0

*tick value
	setv ov04 20

endm



**activate 1 - puts any nearby creatures in the cabin
**(commented out for now, it wasn't obvious this was the way to get creatures into the cabin)
**(there was also no way to eject creatures without taking flight and landing it)
*
*scrp 3 2 50206 1
*
**doif from ne pntr
*
*	gpas 4 0 0 0
*
**endi
*
*endm




*timer - seems to run while the helicopter is flying left or right (not turning) and once just before touching the floor while landing.

scrp 3 2 50206 9


*if we are currently in flight
	doif ov01 eq 1


		doif obst _up_ gt 100

			setv vely -2

		else

			setv vely 0

			addv vely 1

		endi



		doif ov00 eq 0

			setv velx -3

		else

			setv velx 3

		endi


		epas 4 0 0

*reduce boredom a bit. heli rides are fun.
			chem 159 -0.1
*reduce anger a bit. heli rides are relaxing.
			chem 160 -0.1
*reduce crowded a bit. creatures might be stuck in this in close proximity for a while.
			chem 157 -0.1

		next

		targ ownr

	else

		dpas 4 0 0

		tick 0

	endi



endm



*Down

scrp 3 2 50206 1000

	lock

	targ pntr

	anim [0 1 0]

	targ ownr



	doif ov01 eq 1

		loop

			setv vely 3

		untl obst down lt 10

*reset ACCG to default value
		accg ov05

*ACCG was previously being set really high here and never returning to the correct value?
*		accg 10


		doif ov00 eq 0

			anim [2 2 3 3 3 4 4 4 5 5 5 0]

		else

			anim [6 6 7 7 7 8 8 8 9 9 9 1]

		endi



		setv ov01 0

	endi



endm



*Left - Originally anyway... lol

scrp 3 2 50206 1001

	lock

	targ pntr

	anim [0 1 0]

	targ ownr




*If you're facing defaultly(left) go right.

	doif ov00 eq 0

		setv ov00 1

		doif ov01 eq 0

			setv ov01 1

*if you're on the ground...

*grab nearby creatures (touching cabin)
			gpas 4 0 0 1

			anim [2 3 4 5]

			over

			accg ov03

		endi

		pat: kill 1

		pat: kill 2

		pat: kill 3

*pat: kill 4



		part 0

		anim [10 11 12 13 8 1]

		over

		anim [ 6 7 8 9 255]

		inst

		pat: butt 1 "moe_C2toDS_sola" 33 2 180 48 0 [] 1000 0



		pat: butt 2 "moe_C2toDS_sola" 35 2 96 175 0 [] 1001 0



		pat: butt 3 "moe_C2toDS_sola" 37 2 249 176 0 [] 1002 0


*C2 framerate

		part 1
		frat 2
		part 2
		frat 2
		part 3
		frat 2

*pat: dull 4 "moe_C2toDS_sola" 1 0 0 10

		slow



	else

		setv ov00 0

		doif ov01 eq 0

			setv ov01 1

*if you're on the ground...

*grab nearby creatures (touching cabin)
			gpas 4 0 0 1

			anim [6 7 8 9]

			over

			accg ov03

		endi

		pat: kill 1

		pat: kill 2

		pat: kill 3

*		pat: kill 4



		part 0

		anim [1 8 13 12 11 10]

		over

		anim [ 2 3 4 5 255]



		inst

		pat: butt 1 "moe_C2toDS_sola" 32 2 180 48 0 [] 1000 0



		pat: butt 2 "moe_C2toDS_sola" 34 2 257 175 0 [] 1001 0



		pat: butt 3 "moe_C2toDS_sola" 36 2 102 176 0 [] 1002 0


*C2 framerate

		part 1
		frat 2
		part 2
		frat 2
		part 3
		frat 2
		part 0

*pat: dull 4 "moe_C2toDS_sola" 0 0 0 10

		slow



	endi



	tick ov04



endm



*Right - Originally anyway... lol

scrp 3 2 50206 1002

	lock

	targ pntr

	anim [0 1 0]

	targ ownr




*If you're facing defaultly(left) go left...

	doif ov00 eq 0

*setv ov00 0

		doif ov01 eq 0

			setv ov01 1

*if you're on the ground...

*grab nearby creatures (touching cabin)
			gpas 4 0 0 1

			anim [2 3 4 5]

			over

			accg ov03

		endi

		pat: kill 1

		pat: kill 2

		pat: kill 3



*part 0

*anim [10 11 12 13 8 1]

*over

		anim [ 2 3 4 5 255]



		pat: butt 1 "moe_C2toDS_sola" 32 2 180 48 0 [] 1000 0



		pat: butt 2 "moe_C2toDS_sola" 34 2 257 175 0 [] 1001 0



		pat: butt 3 "moe_C2toDS_sola" 36 2 102 176 0 [] 1002 0


*C2 framerate

		part 1
		frat 2
		part 2
		frat 2
		part 3
		frat 2
		part 0

	else

*setv ov00 0

		doif ov01 eq 0

			setv ov01 1

*if you're on the ground...

*grab nearby creatures (touching cabin)
			gpas 4 0 0 1

			anim [6 7 8 9]

			over

			accg ov03

		endi

		pat: kill 1

		pat: kill 2

		pat: kill 3



*part 0

*anim [1 8 13 12 11 10]

*over


		anim [ 6 7 8 9 255]



		pat: butt 1 "moe_C2toDS_sola" 33 2 180 48 0 [] 1000 0



		pat: butt 2 "moe_C2toDS_sola" 35 2 96 175 0 [] 1001 0



		pat: butt 3 "moe_C2toDS_sola" 37 2 249 176 0 [] 1002 0


*C2 framerate

		part 1
		frat 2
		part 2
		frat 2
		part 3
		frat 2
		part 0

	endi



	tick ov04



endm


*goodbye

rscr

enum 3 2 50206

	kill targ

next

scrx 3 2 50206 1
scrx 3 2 50206 9
scrx 3 2 50206 1000
scrx 3 2 50206 1001
scrx 3 2 50206 1002