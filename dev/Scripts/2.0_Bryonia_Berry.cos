*//*Bryonia Berry*//*

* 2 8 50213 - Bryonia Berry
* 2 6 50213 - Bryonia Berry Leaf (maker)

*Matthew, "Moe", '10*
*The one who screams removed empty hunger decrease and made berries invisible until picked.
*NeoDement fixed plane issues
* (if you worked on this file, feel free to add your name!)

new: simp 2 6 50213 "moe_C2toDS_berries" 0 11 5000

attr 16

mvto 4401 48260

tick 1

scrp 2 6 50213 9



	tick rand 100 200



	doif totl 2 8 50213 lt 10

		setv va00 rand 0 1

		doif va00 eq 0

			setv va01 rand 4369 4452

			setv va02 rand 48063 48198

		elif va00 eq 1

			setv va01 rand 4440 4522

			setv va02 rand 48276 48374

		endi

		gsub make_bryonia_berries

	endi



	subr make_bryonia_berries

		new: simp 2 8 50213 "moe_C2toDS_berries" 0 11 rand 1000 3000

		mvsf va01 va02

	retn

endm



scrp 2 8 50213 10

	inst

	perm rand 45 55

	bhvr 48

	accg 0

	attr 211

	elas 10

	fric 50

	tick rand 50 100

*emit 6 .15

endm



scrp 2 8 50213 9

	doif carr eq null

		addv ov00 1



*you don't fall (actually yes you do but only on special occasions)

		doif ov00 ge rand 50 100 and accg ne 4

			emit 6 .15

			attr 195

			accg 4

		endi



		doif ov00 ge rand 125 150

			kill targ

		endi

	endi

endm



*scrp 2 8 50213 4
*
** just gonna set the plane when we create it instead
**	plne rand 4000 6000
*
*endm



scrp 2 8 50213 5

	emit 6 .15

	attr 195

	accg 4

endm



scrp 2 8 50213 6

	sndc "dr12"

endm



scrp 2 8 50213 1

	mesg writ targ 12

endm



scrp 2 8 50213 12

	sndc "chwp"

	stim writ from 78 1

	targ from

*Starch*

*	chem 5 .31

*Hunger for Carbs Decrease*

*	chem 150 -.31

*Prostaglandin*

	chem 94 .19

	targ ownr

	wait 1

	kill targ

endm

rscr

enum 2 6 50213

	kill targ

next

enum 2 8 50213

	kill targ

next

scrx 2 6 50213 9

scrx 2 8 50213 9

scrx 2 8 50213 6

scrx 2 8 50213 5

scrx 2 8 50213 12