* 2 21 50211 - Drums

*NeoDement:

* - Moved hand slap anim from "Pointer Activate 1" to "Activate 1", like with the Balls, just in case...
* - Drums can now be picked up and moved elsewhere


* Left of desert; right of ocean

new: simp 2 21 50211 "moe_C2toDS_triangle" 5 10 800

*mvto 8250 48152
mvto 8250 48145



* Above library

new: simp 2 21 50211 "moe_C2toDS_triangle" 5 10 800

*mvto 3231 49093
mvto 3231 49085


scrp 2 21 50211 10

*	attr 4

*drums can now be picked up
	core 10 102 2 125
	puhl -1 59 42

	attr 198

	elas 0
	accg 5
	aero 5
	perm 51


	bhvr 11

	frat 2

endm



scrp 2 21 50211 1

	stim writ from 97 1

*animate hand slap anim
	doif from eq pntr
		targ from
		anim [4 4 5 5 0]
		targ ownr
	endi

	lock

	snde "dru1"

	anim [0 1 2 3 0]

	unlk

endm



scrp 2 21 50211 2

	stim writ from 97 1

	lock

	setv va00 rand 0 2

	doif va00 eq 0

		snde "dru1"

	elif va00 eq 1

		snde "dru2"

	else

		snde "dru3"

	endi

	anim [0 1 2 3 0]

	unlk

endm



scrp 2 21 50211 3

	stim writ from 97 1

	lock

	setv va00 rand 0 2

	doif va00 eq 0

		snde "dru1"

	elif va00 eq 1

		snde "dru2"

	else

		snde "dru3"

	endi

	anim [0 1 2 3 0]

	unlk

endm




rscr

enum 2 21 50211

	kill targ

next