* 2 21 50210 - Guitar

*NeoDement removed the random Plane, now it's always 3500. Previously it could be up to 50000.


new: simp 2 21 50210 "moe_C2toDS_guitar" 7 0 3500

mvsf 3400 49067


scrp 2 21 50210 10

	attr 199

	bhvr 43

	elas 50

	accg 2

	aero 5

	fric 50

	perm 51

	frat 3

endm



* Activate 1

scrp 2 21 50210 1

	call 1000 from 0

endm



* Activate 2

scrp 2 21 50210 2

	call 1000 from 0

endm



* Hit

scrp 2 21 50210 3

	call 1000 from 0

endm







*shared act1/act/hit script
*(_p1_ is from)

scrp 2 21 50210 1000

	lock


	stim writ _p1_ 97 1


*random sound
	setv va01 rand 0 6



	doif va01 eq 0

		snde "gtr1"

	elif va01 eq 1

		snde "gtr2"

	elif va01 eq 2

		snde "gtr3"

	elif va01 eq 3

		snde "gtr4"

	elif va01 eq 4

		snde "gtr5"

	elif va01 eq 5

		snde "gtr6"

	else

		snde "gtr7"

	endi



*choose anim direction based on holding creature position
	doif crea carr = 1 and _p1_ = carr

		targ carr

		setv va00 posx

		targ ownr

		doif posx gt va00

			setv va00 2

		else

			setv va00 1

		endi

		doif va00 eq 1

			anim [1 2 3 1 2 3 1 2 3 1 2 3]

			wait 40

		elif va00 eq 2

			anim [4 5 6 4 5 6 4 5 6 4 5 6]

			wait 40

		endi
	endi

	pose 0

endm









rscr

enum 2 21 50210

	kill targ

next

scrx 2 21 50210 1

scrx 2 21 50210 2

scrx 2 21 50210 3

scrx 2 21 50210 10