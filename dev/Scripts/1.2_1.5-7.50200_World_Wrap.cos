* This file has scripts for:
*1 5 50200 - World Wrap Camera
*1 6 50200 - World Wrap Grabber
*1 7 50200 - Mouse Pointer Tracker
*1 7 20201 - Window Resize Camera Builder
*1 7 50202 - World Wrap Temporary Object


*todo: probably dont transport any objects that have a creatures attention on them. there would need to be some sort of fail safe if they move too far past the wrap.
*todo: improve speech bubble grabbing. it doesn't check the X coord, currently. also might be able to check if Aiko's Speech Bubbles Upgrade 1.2 is installed which stores which agent created the bubble as an enhancement.


*NeoDement:
*-General cleanup
*-We are now ENUMing over all objects (0 0 0) instead of just objects in family 2 (2 0 0). This means creatures should now transport properly through the wrap. Probably needs a fair bit of testing...
*-The world wrap now handles hand holding properly.
*-Creatures can hear Hand and Agent Help speech over wrap. WIP.



*CA links for the "wrap" rooms.*

*Atmosphere

link grap 2070 47640 grap 10375 47640 100

*Desert Beach

link grap 2070 48090 grap 10375 48090 100

*Biome 1

link grap 2070 48340 grap 10375 48340 100

*Bome 2

link grap 2070 48822 grap 10375 48822 100

*Machines to Volcano

link grap 2070 49340 grap 10375 49340 100

*----------------------*

*Yggdrasil to Desert

link grap 12950 47678 grap 4890 48110 100



*Set the game variables for easy reference.*

setv game "C2toDS_Top" 47600

*unused
*setv game "C2toDS_Bottom" 49999

*unused
*setv game "C2toDS_Left" 0

setv game "C2toDS_Right" 12448

setv game "C2toDS_RoomID" gmap 6753 48383

setv game "C2toDS_WrapLeft" 2047

setv game "C2toDS_WrapRight" 10399

*A value between WrapLeft and WrapRight

setv game "C2toDS_WrapMiddle" 6223

* Wrap padding determines how many panels are pushed back onto the actual non-wrapped part of the world
setv game "C2toDS_WrapPadding" 1

* Wrap Offset - how many pixels to add or remove from an X coordinate to get its wrapped equivalent?
setv game "C2toDS_WrapOffset" 8352



*--//--References---*

*10399 is the point of wrap on the RIGHT side of the map.*

*2048 is the point of wrap on the LEFT side of the map.*

*--//---------------*




*Set up camera heights and widths for easy change.*

*They are 400 wide, 300 high.  This helps for 800x600 displays, the minimum size for Creatures.*

setv game "C2toDS_CamWidth" 400

setv game "C2toDS_CamHeight" 300

*----------------------------------------------------------------------------------*


*mouse pointer tracker

new: simp 1 7 50200 "blank" 1 0 1

attr 0

accg 0

tick 1

fric 100

aero 0

*listen for Raw Key Down (1) + Raw Mouse Down (8). We use them to intercept speech and agent help clicks.
imsk 9

*----------------------------------*

* Window resize watcher/ camera builder

new: simp 1 7 20201 "blnk" 1 0 0

attr 16

* Delay init or right side panels will not be created properly

tick 40


*----------------------------------*


*-Inject the Cameras and Wrap Pieces-*

scrp 1 7 20201 123
	tick 80
endm


scrp 1 7 20201 9

	inst

	lock

	doif wndw eq name "CamWidth::last"

		tick 0

		unlk

*		stop

	endi

	setv name "CamWidth::last" wndw

* Kill existing wrap cameras and grabbers

	enum 1 5 50200
		kill targ
	next

	enum 1 6 50200
		kill targ
	next

*Calculate panels wide

	setv va00 wndw

	setv va11 va00

	divv va11 game "C2toDS_CamWidth"

	addv va11 game "C2toDS_WrapPadding"

	setv va12 va00

	modv va12 game "C2toDS_CamWidth"

	doif va12 gt 0

		addv va11 1

	endi


*Begin counter for injecting Cameras*

	setv va03 0

*Set first Y

	setv va04 game "C2toDS_Top"

	subv va04 299

*Set first X Left

	setv va22 game "C2toDS_WrapLeft"

	addv va22 game "C2toDS_CamWidth"

	subv va22 va00

*Set first X Right

	setv va23 game "C2toDS_WrapRight"

	setv va15 100

	loop

		addv va04 299

		addv va03 1

*Offset the wrap onto the non-wrapped part of the world

		setv va10 game "C2toDS_WrapPadding"

		negv va10

		reps va11


*left side to show right side

*Calculate left side camera x-position

			setv va12 va10

			mulv va12 game "C2toDS_CamWidth"

			addv va12 va22

			setv va14 game "C2toDS_CamWidth"

			doif va12 lt 0

				addv va14 va12

*Ensure that camera has width and can be placed at zero

				doif va14 gt 20

					setv va12 0

				endi

			endi

*Calculate left side camera view x-position

			setv va13 va12

			addv va13 game "C2toDS_WrapOffset"


*Make sure left side camera is in bounds

			doif va12 >= 0

*create world wrap camera
				new: comp 1 5 50200 "moe_C2toDS_camera" 1 1 1

				pat: cmra 1 "moe_C2toDS_camera" 1 0 0 0 va14 game "C2toDS_CamHeight" va14 game "C2toDS_CamHeight"

				attr 0

				accg 0

				fric 100

				aero 0

				mvto va12 va04

				scam targ 1

				cmra va13 va04 0

				setv ov00 va13

				setv ov04 va04

				tick 100

			endi


*Left Side Grabber

			new: simp 1 6 50200 "moe_C2toDS_Camera" 1 1 1

			attr 0

			accg 0

			tick 10

			fric 100

			aero 0

			mvto va12 va04

*I Am on the right of the world, in the 10000 range*

			sets name "WhichWrapPieceAmI?" "Left"



*right side to show left side

*Calculate right side camera agent x

			setv va12 va10

			mulv va12 game "C2toDS_CamWidth"

			addv va12 va23

*Calculate camera position focus x

			setv va13 va12

			subv va13 game "C2toDS_WrapOffset"

*Make sure camera is still in bounds

			setv va14 va12

			addv va14 game "C2toDS_CamWidth"

			doif va14 gt game "C2toDS_Right"

				setv va15 va14

				subv va15 game "C2toDS_Right"

			else

				setv va15 game "C2toDS_CamWidth"

			endi

			setv va14 va12

			addv va14 va15


			doif va15 gt 20 and va14 le game "C2toDS_Right"

				new: comp 1 5 50200 "moe_C2toDS_camera" 1 1 1

				pat: cmra 1 "moe_C2toDS_camera" 1 0 0 0 va15 game "C2toDS_CamHeight" va15 game "C2toDS_CamHeight"

				attr 0

				accg 0

				fric 100

				aero 0

				mvto va12 va04

				scam targ 1

				cmra va13 va04 0

				setv ov00 va13

				setv ov04 va04

				tick 100

			endi

*----------*

*Right Side Grabber

			new: simp 1 6 50200 "moe_C2toDS_Camera" 1 1 1

			attr 0

			accg 0

			tick 10

			fric 100

			aero 0

			mvto va12 va04

			sets name "WhichWrapPieceAmI?" "Right"

*Increment X-wide

			addv va10 1

		repe

	untl va03 eq 8

	unlk

endm



*----END INJECT CAMERAS AND WRAP PIECES----*



*This might be the cause of a mild performance hit, but it keeps the cameras from blacking out.*

scrp 1 5 50200 9

	tick 1000

	scam targ 1

	cmra ov00 ov04 0

endm



* World Wrap Grabber - World Loaded script

*This might be the cause of a mild performance hit, but it keeps the cameras from blacking out.*

scrp 1 5 50200 128

	scam targ 1

	cmra ov00 ov04 0

endm






* World Wrap Grabber - Timer script (runs once every 10 ticks)

scrp 1 6 50200 9

*stop

	inst

	doif name "WhichWrapPieceAmI?" eq "Right"

		etch 0 0 0

*if the target isn't null, has attribute Suffer Physics, is Autonomous and is over the Right hand side of the wrap
			doif targ ne null and targ ne pntr and attr ge 64 and movs eq 0 and posx gt game "C2toDS_WrapRight"

*if the target is an object
				doif crea targ ne 1

					setv va00 posl
					setv va01 post

					addv va01 1

					subv va00 game "C2toDS_WrapOffset"

*move the object to the other side of the wrap

*do an unsafe move if possible
					doif tmvt va00 va01 = 1
						mvto va00 va01
					else
						mvsf va00 va01
					endi

*if the target is a creature
				else

					setv va00 dftx
					setv va01 dfty

					addv va01 1

					subv va00 game "C2toDS_WrapOffset"


*check if the target creature is holding the players hand for camera reasons
					doif targ eq hhld

*remember current camera coordinates before we jump
						setv va02 cmrx
						setv va03 cmry
*offset them to the other side of the world
						subv va02 game "C2toDS_WrapOffset"

					endi


*move the creature to the other side of the wrap
*(do an unsafe move if possible)
					doif tmvf va00 va01 = 1
						mvft va00 va01
					else
						mvsf va00 va01
					endi

*if we have stored some camera coordinates to jump to (and they're not out of bounds)
					doif va02 > 0 and va03 > 0 and va02 < game "C2toDS_Right"

*move camera to equivalent coordinate on other side of world
*(the automatic camera reposition is offset as if you dragged the creature to the far edge of the window)
						cmrp va02 va03 0

					endi

				endi

			endi

		next

	elif name "WhichWrapPieceAmI?" eq "Left"

		etch 0 0 0

*if the target isn't null, has attribute Suffer Physics, is Autonomous and is over the Left hand side of the wrap
			doif targ ne null and targ ne pntr and attr ge 64 and movs eq 0 and posx lt game "C2toDS_WrapLeft"

*if the target is an object
				doif crea targ ne 1

					setv va00 posl
					setv va01 post

					addv va01 1

					addv va00 game "C2toDS_WrapOffset"

*move the object to the other side of the wrap

*do an unsafe move if possible
					doif tmvt va00 va01 = 1
						mvto va00 va01
					else
						mvsf va00 va01
					endi

*if the target is a creature
				else

					setv va00 dftx
					setv va01 dfty

					addv va01 1

					addv va00 game "C2toDS_WrapOffset"


*check if the target creature is holding the players hand for camera reasons
					doif targ eq hhld

*remember current camera coordinates before we jump
						setv va02 cmrx
						setv va03 cmry
*offset them to the other side of the world
						addv va02 game "C2toDS_WrapOffset"

					endi


*move the creature to the other side of the wrap
*(do an unsafe move if possible)
					doif tmvf va00 va01 = 1
						mvft va00 va01
					else
						mvsf va00 va01
					endi

*if we have stored some camera coordinates to jump to (and they're not out of bounds)
					doif va02 > 0 and va03 > 0 and va02 < game "C2toDS_Right"

*move camera to equivalent coordinate on other side of world
*(the automatic camera reposition is offset as if you dragged the creature to the far edge of the window)
						cmrp va02 va03 0

					endi

				endi

			endi

		next

	endi

endm



*Pointer Tracker - Timer Script, runs once per tick*

scrp 1 7 50200 9

*stop

	inst

*grab the pointer so we can check where it is
	targ pntr

*remember the pointer coords
	setv va00 posl
	setv va01 post

	doif game "C2toDS_RoomID" = gmap va00 va01

*if it's too far off the left side, we need to wrap the camera round to the right
		doif va00 <= game "C2toDS_WrapLeft"

*don't automatically move the camera if we're holding a creatures hand, let the World Wrap Grabber timer script handle it
			targ hhld
			doif crea targ ne 1

				scam null 0

				setv va04 wndl

				addv va04 game "C2toDS_WrapOffset"

				setv va05 wndt

				doif gmap va04 va05 <> -1

					cmra va04 va05 0

				endi

			endi

		endi


*if it's too far off the right side, we need to wrap the camera round to the left
		doif va00 >= game "C2toDS_WrapRight"

*don't automatically move the camera if we're holding a creatures hand, let the World Wrap Grabber timer script handle it
			targ hhld
			doif crea targ ne 1

				scam null 0

				setv va04 wndl

				subv va04 game "C2toDS_WrapOffset"

				setv va05 wndt

				doif gmap va04 va05 <> -1

					cmra va04 va05 0

				endi

			endi

		endi

	endi

endm





*Pointer Tracker - Raw Mouse Down script
*This copies the effect of agent help bubbles over the world wrap.

scrp 1 7 50200 76

	inst

*check if "agent help watcher" IMSK is set to 9. If it is, that means agent help mode is active.
	rtar 1 2 4
	doif imsk <> 9
		stop
	endi

	seta va05 hots
	doif va05 <> null

* left mouse - shout your name
		doif _p1_ = 1
			targ va05


*store XY coords for later
			setv va00 posl
			setv va01 post

*fake temp object needs to have the same category as the real one
			setv va50 cata
*and also the same visibility setting according to attr
			setv va51 attr


*stop the script if we're not in the C2toDS metaroom
			doif game "C2toDS_RoomID" <> gmap va00 va01
				stop
			endi

*		set up string with name of object
			setv va98 cata
			sets va99 catx va98

* icons gui
			doif fmly eq 1 and gnus eq 2 and spcs eq 14
				stop
* agent help box
			elif fmly eq 1 and gnus eq 2 and spcs eq 5
				stop
			else
* unclassified (39) things don't say their name
				doif va98 <> -1

					targ va05

					new: simp 1 7 50202 "blnk" 1 0 0

*fake temp object needs to have the same category as the real one
					cato va50
*and also the same visibility setting according to attr
					doif va51 >= 16
						attr 16
					else
						attr 0
					endi

*check which side of the wrap the object is on and add or remove the appropriate offset
					doif va00 <= game "C2toDS_WrapRight" and va00 > game "C2toDS_WrapMiddle"

						subv va00 game "C2toDS_WrapOffset"

					elif va00 >= game "C2toDS_WrapLeft" and va00 < game "C2toDS_WrapMiddle"

						addv va00 game "C2toDS_WrapOffset"

					else
						stop
					endi

					mvto va00 va01

*					seta va06 targ
*
** send message to bubble factory to do visuals
*					rtar 1 2 10
*					mesg wrt+ targ 126 va99 va06 0
** target agent again, and send shout from it
*					targ va06

					ordr shou va99

					kill targ

				endi
			endi
		endi
	endi

endm








*A keypress
scrp 1 7 50200 73

	lock
*if the user pressed enter (or control S)
	doif _p1_ eq 'S' or _p1_ eq 13
*WHY IS THIS NEEDED. IT MAKES NO SENSE.
*(I believe it's waiting until the start of the next tick because the Key Down event takes place before the speech bubble exiss)
		wait 0
		targ ownr
		inst

		sets va00 ""


*grab the pointer so we can check where it is
		targ pntr

*remember the pointer coords
		setv va00 posl
		setv va01 post

		doif game "C2toDS_RoomID" = gmap va00 va01

			setv va10 va00
			setv va11 va01
			addv va10 10
			addv va11 10

*scan speech bubbles
			enum 1 2 9

*				dbg: outs "va00:"
*				dbg: outs vtos va00
*				dbg: outs "va10:"
*				dbg: outs vtos va10
*				dbg: outs "posr:"
*				dbg: outs vtos posr


*todo: not accounting for X pos yet because sometimes speech bubble draws on other side.
*and posr > va00 and posr < va10

				doif posb > va01 and posb < va11

*set the ptxt in va02 for now
					part 1
					doif targ ne null
						sets va02 ptxt


*make sure the hand said something and didn't just press enter by mistake
						doif strl va02 > 1
							inst

*make fake hand with same classifier as real one so norns can't tell the difference
							new: simp 2 1 1 "blnk" 1 0 0

							seta va99 targ

*this temporary object acts as an auto-delete timer for the fake hand created above
							new: simp 1 7 50202 "blnk" 1 0 0
							tick 20
							seta ov00 va99

*switch back to fake hand for the rest of this script
							targ va99

*check which side of the wrap the pointer is on and add or remove the appropriate offset
							doif va00 <= game "C2toDS_WrapRight" and va00 > game "C2toDS_WrapMiddle"

								subv va00 game "C2toDS_WrapOffset"

							elif va00 >= game "C2toDS_WrapLeft" and va00 < game "C2toDS_WrapMiddle"

								addv va00 game "C2toDS_WrapOffset"

							else
								stop
							endi

							mvto va00 va01

*todo: could check if it starts with "maybe " or "me " (if hand name ISNT "me") or other stuff (aka detect if its probably from a creature) and ignore it if it is
*this will be challenging because of other languages.


*fake hand shouts out an order

							ordr shou va02

*test string to show that indeed, creatures can hear it

*							ordr shou "perfect noun 2 hand"


** send message to bubble factory to do visuals
*							seta va03 targ
*
*							rtar 1 2 10
*
*							mesg wrt+ targ 126 va02 va03 0
*
*							targ ownr

						endi

					endi

				endi

			next

		endi

	endi

endm




*World Wrap Temporary Object Timer Script - used to automatically remove the fake hand after a while

scrp 1 7 50202 9

	inst

*but only if you're not any creature's IT!

	setv va00 0

	enum 4 0 0
		doif aslp = 0 and dead = 0 and targ <> null
			doif mv00 <> _it_ and mv00 <> iitt
				kill mv00
				kill ownr
				stop
			endi
		endi
		addv va00 1
	next

*automatically delete yourself if there are no creatures in the world	
	doif va00 = 0
		kill mv00
		kill ownr
	endi

endm





rscr


*Delete Cameras*

enum 1 5 50200

	kill targ

next

*Delete Wrap Pieces*

enum 1 6 50200

	kill targ

next

*Delete Pointer Tracker*

enum 1 7 50200

	kill targ

next


*Delete Pointer Hand Hold Watcher

enum 1 7 20202

	kill targ

next



scrx 1 5 50200 9

scrx 1 5 50200 128

scrx 1 6 50200 9

scrx 1 7 50200 9

scrx 1 7 50201 9

scrx 1 7 50201 123

scrx 1 7 50202 9


*Remove "wrap" room links*

doif grap 2070 4764 ne -1 and link grap 2070 47640 grap 10375 47640 ne 0

*Atmosphere

	link grap 2070 47640 grap 10375 47640 0

*Desert Beach

	link grap 2070 48090 grap 10375 48090 0

*Biome 1

	link grap 2070 48340 grap 10375 48340 0

*Biome 2

	link grap 2070 48822 grap 10375 48822 0

*Machines to Volcano

	link grap 2070 49340 grap 10375 49340 0

endi