* This file has scripts for:
*1	5	50200	World Wrap Camera
*1	6	50200	World Wrap Grabber
*1	7	50200	Mouse Pointer Tracker
*1  7   20201   Window Resize Camera Builder


*CA links for the "wrap" rooms.*

*Atmosphere

link grap 2070 47640 grap 10375 47640 100

*Desert Beach

link grap 2070 48090 grap 10375 48090 100

*Biome 1

link grap 2070 48340 grap 10375 48340 100

*Bome 2

link grap 2070 48822 grap 10375 48822 100

*Machines to Volcano

link grap 2070 49340 grap 10375 49340 100

*----------------------*

*Yggdrasil to Desert

link grap 12950 47678 grap 4890 48110 100

*Set the game variables for easy reference.*

setv game "C2toDS_Top" 47600

*unused
*setv game "C2toDS_Bottom" 49999

*unused
*setv game "C2toDS_Left" 0

setv game "C2toDS_Right" 12448

setv game "C2toDS_RoomID" gmap 6753 48383

setv game "C2toDS_WrapLeft" 2047

setv game "C2toDS_WrapRight" 10399

* Wrap padding determines how many panels are pushed back onto the actual non-wrapped part of the world

setv game "C2toDS_WrapPadding" 1

* Wrap Offset - how many pixels to add or remove from an X coordinate to get its wrapped equivalent?
setv game "C2toDS_WrapOffset" 8352



*--//--References---*

*10399 is the point of wrap on the RIGHT side of the map.*

*2048 is the point of wrap on the LEFT side of the map.*

*--//---------------*




*Set up camera heights and widths for easy change.*

*They are 400 wide, 300 high.  This helps for 800x600 displays, the minimum size for Creatures.*

setv game "C2toDS_CamWidth" 400

setv game "C2toDS_CamHeight" 300

*----------------------------------------------------------------------------------*


*mouse pointer tracker

new: simp 1 7 50200 "blank" 1 0 1

attr 0

accg 0

tick 1

fric 100

aero 0


*----------------------------------*

* Window resize watcher/ camera builder

new: simp 1 7 20201 "blnk" 1 0 0

attr 16

* Delay init or right side panels will not be created properly

tick 40


*----------------------------------*


*-Inject the Cameras and Wrap Pieces-*

scrp 1 7 20201 123
	tick 80
endm


scrp 1 7 20201 9

	inst

	lock

	doif wndw eq name "CamWidth::last"

		tick 0

		unlk

		stop

	endi

	setv name "CamWidth::last" wndw

* Kill existing wrap cameras and grabbers

	enum 1 5 50200
		kill targ
	next

	enum 1 6 50200
		kill targ
	next

*Calculate panels wide

	setv va00 wndw

	setv va11 va00

	divv va11 game "C2toDS_CamWidth"

	addv va11 game "C2toDS_WrapPadding"

	setv va12 va00

	modv va12 game "C2toDS_CamWidth"

	doif va12 gt 0

		addv va11 1

	endi


*Begin counter for injecting Cameras*

	setv va03 0

*Set first Y

	setv va04 game "C2toDS_Top"

	subv va04 299

*Set first X Left

	setv va22 game "C2toDS_WrapLeft"

	addv va22 game "C2toDS_CamWidth"

	subv va22 va00

*Set first X Right

	setv va23 game "C2toDS_WrapRight"

	setv va15 100

	loop

		addv va04 299

		addv va03 1

*Offset the wrap onto the non-wrapped part of the world

		setv va10 game "C2toDS_WrapPadding"

		negv va10

		reps va11


*left side to show right side

*Calculate left side camera x-position

			setv va12 va10

			mulv va12 game "C2toDS_CamWidth"

			addv va12 va22

			setv va14 game "C2toDS_CamWidth"

			doif va12 lt 0

				addv va14 va12

*Ensure that camera has width and can be placed at zero

				doif va14 gt 20

					setv va12 0

				endi

			endi

*Calculate left side camera view x-position

			setv va13 va12

			addv va13 game "C2toDS_WrapOffset"


*Make sure left side camera is in bounds

			doif va12 >= 0

*create world wrap camera
				new: comp 1 5 50200 "moe_C2toDS_camera" 1 1 1

				pat: cmra 1 "moe_C2toDS_camera" 1 0 0 0 va14 game "C2toDS_CamHeight" va14 game "C2toDS_CamHeight"

				attr 0

				accg 0

				fric 100

				aero 0

				mvto va12 va04

				scam targ 1

				cmra va13 va04 0

				setv ov00 va13

				setv ov04 va04

				tick 100

			endi


*Left Side Grabber

			new: simp 1 6 50200 "moe_C2toDS_Camera" 1 1 1

			attr 0

			accg 0

			tick 10

			fric 100

			aero 0

			mvto va12 va04

*I Am on the right of the world, in the 10000 range*

			sets name "WhichWrapPieceAmI?" "Left"



*right side to show left side

*Calculate right side camera agent x

			setv va12 va10

			mulv va12 game "C2toDS_CamWidth"

			addv va12 va23

*Calculate camera position focus x

			setv va13 va12

			subv va13 game "C2toDS_WrapOffset"

*Make sure camera is still in bounds

			setv va14 va12

			addv va14 game "C2toDS_CamWidth"

			doif va14 gt game "C2toDS_Right"

				setv va15 va14

				subv va15 game "C2toDS_Right"

			else

				setv va15 game "C2toDS_CamWidth"

			endi

			setv va14 va12

			addv va14 va15


			doif va15 gt 20 and va14 le game "C2toDS_Right"

				new: comp 1 5 50200 "moe_C2toDS_camera" 1 1 1

				pat: cmra 1 "moe_C2toDS_camera" 1 0 0 0 va15 game "C2toDS_CamHeight" va15 game "C2toDS_CamHeight"

				attr 0

				accg 0

				fric 100

				aero 0

				mvto va12 va04

				scam targ 1

				cmra va13 va04 0

				setv ov00 va13

				setv ov04 va04

				tick 100

			endi

*----------*

*Right Side Grabber

			new: simp 1 6 50200 "moe_C2toDS_Camera" 1 1 1

			attr 0

			accg 0

			tick 10

			fric 100

			aero 0

			mvto va12 va04

			sets name "WhichWrapPieceAmI?" "Right"

*Increment X-wide

			addv va10 1

		repe

	untl va03 eq 8

	unlk

endm



*----END INJECT CAMERAS AND WRAP PIECES----*



*This might be the cause of a mild performance hit, but it keeps the cameras from blacking out.*

scrp 1 5 50200 9

	tick 1000

	scam targ 1

	cmra ov00 ov04 0

endm



* World Wrap Grabber - World Loaded script

*This might be the cause of a mild performance hit, but it keeps the cameras from blacking out.*

scrp 1 5 50200 128

	scam targ 1

	cmra ov00 ov04 0

endm






* World Wrap Grabber - Timer script

scrp 1 6 50200 9



	inst

	doif name "WhichWrapPieceAmI?" eq "Right"

*		etch 2 0 0
		etch 0 0 0

			doif targ ne null

				doif targ ne null and targ ne pntr and attr ge 64 and movs eq 0 and posx gt game "C2toDS_WrapRight"

					doif crea targ ne 1

						setv va00 posl

						setv va01 post

						addv va01 5

						subv va00 8351

*move the object to the other side of the wrap

*do an unsafe move if possible
						doif tmvt va00 va01 = 1
							mvto va00 va01
						else
							mvsf va00 va01
						endi

					else

						setv va00 dftx

						setv va01 dfty

						addv va01 1


						subv va00 8351

*move the creature to the other side of the wrap

*do an unsafe move if possible
						doif tmvf va00 va01 = 1
							mvft va00 va01
						else
							mvsf va00 va01
						endi

					endi

				endi

			endi

		next

	elif name "WhichWrapPieceAmI?" eq "Left"

*	etch 2 0 0
		etch 0 0 0

			doif targ ne null and targ ne pntr and attr ge 64 and movs eq 0 and posx lt game "C2toDS_WrapLeft"

				doif crea targ ne 1

					setv va00 posl

					setv va01 post

					addv va01 5

					addv va00 game "C2toDS_WrapOffset"

*move the object to the other side of the wrap

*do an unsafe move if possible
					doif tmvt va00 va01 = 1
						mvto va00 va01
					else
						mvsf va00 va01
					endi

				else

					setv va00 dftx

					setv va01 dfty

					addv va01 1

					addv va00 game "C2toDS_WrapOffset"

*do an unsafe move if possible
					doif tmvf va00 va01 = 1
						mvft va00 va01
					else
						mvsf va00 va01
					endi

				endi


			endi



		next



	endi

endm



*Pointer Tracker - Timer Script, runs once per tick*

scrp 1 7 50200 9

	inst

*grab the pointer so we can check where it is
	targ pntr

*remember the pointer coords
	setv va00 posl
	setv va01 post

	inst

	doif game "C2toDS_RoomID" = gmap va00 va01

*if it's too far off the left side, we need to wrap the camera round to the right
		doif va00 <= game "C2toDS_WrapLeft"

*let go of any creatures hand first
			targ hhld
			doif crea targ eq 1
				nohh

*				remember this creature target in va99!
				seta va99 targ
			endi

			scam null 0

			setv va04 wndl

			addv va04 game "C2toDS_WrapOffset"

			setv va05 wndt

			doif gmap va04 va05 <> -1

				cmra va04 va05 0

			endi

*hold hands with the creature again now we're on the other side of the map
*todo: get this working
*			doif crea targ eq 1
*				wait 1
*				mesg writ targ 13
*			endi

		endi


*if it's too far off the right side, we need to wrap the camera round to the left
		doif va00 >= game "C2toDS_WrapRight"

*let go of the creatures hand first
			targ hhld
			doif crea targ eq 1
				nohh

*				remember this creature target for the hand hold watcher
				seta va99 targ
			endi

			scam null 0

			setv va04 wndl

			subv va04 game "C2toDS_WrapOffset"

			setv va05 wndt

			doif gmap va04 va05 <> -1

				cmra va04 va05 0

			endi

*hold hands with the creature again now we're on the other side of the map
*todo: get this working
*			doif crea targ eq 1
*				wait 1
*				mesg writ targ 13
*			endi

		endi

	endi

endm

*P-------------r*



rscr


*Delete Cameras*

enum 1 5 50200

	kill targ

next

*Delete Wrap Pieces*

enum 1 6 50200

	kill targ

next

*Delete Pointer Tracker*

enum 1 7 50200

	kill targ

next


*Delete Pointer Hand Hold Watcher

enum 1 7 20202

	kill targ

next



scrx 1 5 50200 9

scrx 1 5 50200 128

scrx 1 6 50200 9

scrx 1 7 50200 9

scrx 1 7 50201 9

scrx 1 7 50201 123


*Remove "wrap" room links*

doif grap 2070 4764 ne -1 and link grap 2070 47640 grap 10375 47640 ne 0

*Atmosphere

	link grap 2070 47640 grap 10375 47640 0

*Desert Beach

	link grap 2070 48090 grap 10375 48090 0

*Biome 1

	link grap 2070 48340 grap 10375 48340 0

*Biome 2

	link grap 2070 48822 grap 10375 48822 0

*Machines to Volcano

	link grap 2070 49340 grap 10375 49340 0

endi